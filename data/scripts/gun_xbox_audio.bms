# extract the contents of the music_pcm and stream_pcm packets of Gun (Xbox)
# (c) 2012-05-30 by AlphaTwentyThree of XeNTaX

get BNAME basename
set NAME0 BNAME
set NAME1 BNAME
set NAME2 BNAME
string NAME0 += "0.wad"
string NAME1 += "1.wad"
string NAME2 += "2.wad"

open FDSE NAME0 1
open FDSE NAME1 2
if BNAME == "stream_pcm"
   open FDSE NAME2 3
endif
log MEMORY_FILE 0 0

idstring "PDAW" 0
get UNK long 0
get FILES long 0
get UNK long 0
for i = 1 <= FILES
   get NAME_CRC long 0
   get FILE long 0
   get OFFSET long 0
   get SIZE long 0
   get CODE short 0
   get CH short 0
   get FREQ long 0
   get UNK long 0
   get INTERLEAVE short 0
   get BITS short 0 # always 4 (Xbox ADPCM)
   get UNK long 0
   set NAME BNAME
   string NAME += "_"
   string NAME += i
   string REST p= "_0x%08x.lwav" NAME_CRC
   string NAME += REST
   if FILE == 0
      log MEMORY_FILE2 OFFSET SIZE 1
   elif FILE == 1
      log MEMORY_FILE2 OFFSET SIZE 2
   elif FILE == 2
      log MEMORY_FILE2 OFFSET SIZE 3
   endif
   savepos MYOFF 0
   callfunction XADP 1
   goto MYOFF 0
next i

startfunction XADP
   set MEMORY_FILE binary "\x52\x49\x46\x46\x18\x51\xa3\x0\x57\x41\x56\x45\x66\x6d\x74\x20\x14\x0\x0\x0\x69\x0\x2\x0\x44\xac\x0\x0\xcc\xc1\x0\x0\x48\x0\x4\x0\x2\x0\x40\x0\x64\x61\x74\x61\xf0\x50\xa3\x0"
   set RIFFSIZE SIZE
   math RIFFSIZE += 0x28
   putVarChr MEMORY_FILE 0x04 RIFFSIZE long
   putVarChr MEMORY_FILE 0x16 CH byte
   putVarChr MEMORY_FILE 0x18 FREQ long
   set VAR1 0x3073
   math VAR1 *= CH
   if FREQ == 48000
      math VAR1 *= 44100
   else
      math VAR1 *= FREQ
   endif
   math VAR1 /= 22050
   putVarChr MEMORY_FILE 0x1c VAR1 long   
   set VAR2 0x24
   math VAR2 *= CH
   putVarChr MEMORY_FILE 0x20 VAR2 short
   putVarChr MEMORY_FILE 0x2c SIZE long
   append
   log MEMORY_FILE 0 SIZE MEMORY_FILE2
   append
   if NAME == ""
      get NAME basename
      string NAME += ".lwav"
   endif
   get SIZE asize MEMORY_FILE
   log NAME 0 SIZE MEMORY_FILE
endfunction