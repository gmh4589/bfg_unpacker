# Carrier Command Gaea Mission
# script for QuickBMS http://quickbms.aluigi.org

endian big
idstring "FORM"
get OFFSET long
getdstring DUMMY 4

get PAK_SIZE asize
savepos OFFSET
for OFFSET = OFFSET < PAK_SIZE
    getdstring TYPE 4
    get SIZE long
    savepos OFFSET
    if TYPE == "FILE"
        math FILE_OFFSET = OFFSET
        math FILE_SIZE = SIZE
    endif
    math OFFSET += SIZE
    goto OFFSET
next

endian little
goto FILE_OFFSET
math FILE_SIZE += FILE_OFFSET
set PATH string ""
set NAME string ""
for FILE_OFFSET = FILE_OFFSET < FILE_SIZE
    callfunction EXTRACT
    savepos FILE_OFFSET
next

startfunction EXTRACT
    string PATH += NAME
    string PATH += /
    get TYPE byte
    get NAMESZ byte
    getdstring NAME NAMESZ
    if TYPE == 0
        get FILES long
        for i = 0 < FILES
        callfunction EXTRACT
        next i
    else
        get OFFSET long
        get ZSIZE long
        get SIZE long
        get DUMMY1 long
        get FLAGS long

        set FNAME string PATH
        string FNAME += NAME
        if FLAGS & 0x04000000   # or 0x02000000
            clog FNAME OFFSET ZSIZE SIZE
        else
            log FNAME OFFSET SIZE
        endif
    endif
endfunction
