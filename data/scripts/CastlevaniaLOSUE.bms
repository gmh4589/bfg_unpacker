# Castlevania: Lords of Shadow - Ultimate Edition (PC-DEMO) (DAT format) 0.3
#
# Written by Ekey (h4x0r)
# http://forum.xentax.com
#
# script for QuickBMS http://quickbms.aluigi.org

idstring "BFPK"
get VERSION long
get TABLESIZE long
math TABLESIZE += 16
savepos TABLEOFFSET

callfunction TableDecrypt 1

getdstring TRASH 0x10 MEMORY_FILE
get FILES long MEMORY_FILE

for i = 0 < FILES
    get NSIZE long MEMORY_FILE
    getdstring NAME NSIZE MEMORY_FILE
    get SIZE long MEMORY_FILE
    get OFFSET long MEMORY_FILE
   
    if VERSION = 2
        log NAME OFFSET SIZE
    elseif VERSION = 3
        goto OFFSET
        get ZSIZE long
        savepos OFFSET
        clog NAME OFFSET ZSIZE SIZE
    endif
next i

startfunction TableDecrypt
    encryption aes_256_cbc "\x50\x43\x56\x80\x72\x73\xEE\x6F\xF1\x44\xF3\x6E\xEA\xDF\x79\x43\x6C\x69\x6D\x61\x78\x53\x74\x75\x64\x69\x6F\x73\x32\x30\x31\x33"
    log MEMORY_FILE TABLEOFFSET TABLESIZE
    encryption "" ""
endfunction