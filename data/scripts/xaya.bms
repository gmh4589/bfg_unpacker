# Xaya3D screensavers
#   Candice, Three of Xaya, Blossom Fairy, Burning Sun, Virtual Earth, Dark Forest, Strip Dance, Sleeping Girl and more
# script for QuickBMS http://quickbms.aluigi.org

quickbmsver 0.3.15
get NAMESZ long
getdstring NAME NAMESZ
savepos OFFSET
get SIZE asize
math SIZE -= OFFSET
log MEMORY_FILE OFFSET SIZE

putvarchr MEMORY_FILE2 SIZE 0
log MEMORY_FILE2 0 SIZE MEMORY_FILE # for the remaining bytes

math e470 = 0
math e45c = SIZE
math e45c v= 2
for e46c = 0 < e45c
    for e468 = 0 < e45c
        math eax = e468
        math eax *= e45c
        math eax += e46c
        getvarchr BYTE MEMORY_FILE eax
        putvarchr MEMORY_FILE2 e470 BYTE
        math e470 += 1
    next e468
next e46c

goto 0 MEMORY_FILE2
get FILES long MEMORY_FILE2
for i = 0 < FILES
    get IDX long MEMORY_FILE2
    putarray 0 i IDX
next i

get FILES long MEMORY_FILE2
for i = 0 < FILES
    get OFFSET long MEMORY_FILE2
    putarray 1 i OFFSET
next i

get FILES long MEMORY_FILE2
for i = 0 < FILES
    get SIZE long MEMORY_FILE2
    putarray 2 i SIZE
next i

get FILES long MEMORY_FILE2
for i = 0 < FILES
    get TYPE long MEMORY_FILE2
    putarray 3 i TYPE
next i
savepos BASE_OFF MEMORY_FILE2

for i = 0 < FILES
    getarray IDX 0 i
    getarray OFFSET 1 IDX
    getarray SIZE 2 IDX
    getarray TYPE 3 IDX
    math OFFSET += BASE_OFF
    set NAME string IDX
    if TYPE == 1061
        string NAME += ".jpg"
    elif TYPE == 1066
        string NAME += ".png"
    elif TYPE == 0xffffffff
        string NAME += ".xa3"
    else
        string NAME += ".dat"
    endif
    log NAME OFFSET SIZE MEMORY_FILE2
next i
