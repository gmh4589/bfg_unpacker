# Ashes Cricket 2009 (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

comtype deflate
get ZERO long
get ZERO long
get FILES long

math SIZE = FILES
math SIZE *= 28
savepos OFFSET
log MEMORY_FILE OFFSET SIZE

for i = 0 < FILES
    get NAME_CRC long MEMORY_FILE
    get OFFSET longlong MEMORY_FILE
    get SIZE long MEMORY_FILE
    get ZSIZE long MEMORY_FILE
    get DUMMY long MEMORY_FILE  # type?
    get DUMMY long MEMORY_FILE  # don't know
    math OFFSET <<= 4
    if ZSIZE == 0
        log "" OFFSET SIZE
    else
        callfunction unashes
        log "" 0 SIZE MEMORY_FILE2
    endif
next i

startfunction unashes
    goto OFFSET
    get CHUNKS long
    math CHUNKS -= 1
    for x = 0 < CHUNKS  # I don't need them
        get CHUNK_BLOCK_ZSIZE long
    next x
    savepos OFFSET

    math CHUNKS = SIZE
    math CHUNKS /= 65536
    if SIZE % 65536
        math CHUNKS += 1
    endif
    math TMP = CHUNKS
    math TMP *= 2   # short
    math OFFSET += TMP

    putvarchr MEMORY_FILE2 SIZE 0
    log MEMORY_FILE2 0 0
    append
    for x = 0 < CHUNKS
        get CHUNKSZ short
        math OFFSET x= 16   # aligned
        if CHUNKSZ == 0     # correct?
            log MEMORY_FILE2 OFFSET 65536
        else
            clog MEMORY_FILE2 OFFSET CHUNKSZ 65536
        endif
        math OFFSET += CHUNKSZ
    next x
    append
endfunction
