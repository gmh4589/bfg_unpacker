# Psi-Ops: The Mindgate Conspiracy Midway MSX (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

get EXT extension
if EXT != "msx"
    print "Error: you must open the MSX files"
    cleanexit
endif

get VER long    # 2
get FILES long
get BASE_OFF long
get NAMES_OFF long
get BASE_OFF2 long
get MSX_SIZE long
get ZERO long
for i = 0 < FILES
    get ID long
    get OFFSET long
    get SIZE long
    get HEAD_OFF long
    get HEAD_SIZE long
    get IS_RELATIVE long

    savepos TMP
    goto NAMES_OFF
    get NAME string
    savepos NAMES_OFF
    goto TMP

    if NAME == ""
        string NAME p "%08x.wav" i
    endif

    if HEAD_OFF != 0    # the first file is all zeroes with an invalid offset
        if IS_RELATIVE != 0
            math OFFSET + BASE_OFF
        endif
        log NAME HEAD_OFF HEAD_SIZE
        log MEMORY_FILE 0 0
        putdstring "data" 4 MEMORY_FILE
        put SIZE long MEMORY_FILE
        append
        log NAME 0 8 MEMORY_FILE
        log NAME OFFSET SIZE
        append
    endif
next i
