# Monsters, Inc. (PS2) BNK/SAD
# script for QuickBMS http://quickbms.aluigi.org

getdstring SIGN 4
goto 0
if SIGN == "SBNK"
    set NAME string ""
    callfunction EXTRACT_SBNK 1
    cleanexit
endif

idstring "SWAD"
get FILES long
for i = 0 < FILES
    get NAME_OFF long
    get OFFSET long
    get SIZE long
    get ZERO long

    savepos TMP
    goto NAME_OFF
    get NAME string
    goto OFFSET
    getdstring SIGN 4
    if SIGN == "SBNK"
        string NAME + "/."
        goto OFFSET
        callfunction EXTRACT_SBNK
    else
        log NAME OFFSET SIZE
    endif
    goto TMP
next i

startfunction EXTRACT_SBNK
    savepos TMP_OFF
    idstring "SBNK"
    get DUMMY short
    get DUMMY short
    get DUMMY short
    get ENTRIES short
    get DUMMY short
    get SAMPLES short
    get OFFSET1 long
    get OFFSET2 long
    get OFFSET3 long
    get OFFSET4 long
    get OFFSET5 long
    math OFFSET5 + TMP_OFF
    goto OFFSET5
    idstring "SAMP"
    get LAST_OFF long
    for i = 0 < SAMPLES
        get OFFSET long
        putarray 0 i OFFSET
    next i
    savepos BASE_OFF
    sortarray 0 1
    for i = 0 <= SAMPLES
        if i == 0
            getarray OFFSET 0 0
        else
            if i == SAMPLES
                math NEXT_OFF = LAST_OFF
            else
                getarray NEXT_OFF 0 i
            endif
            xmath SIZE "NEXT_OFF - OFFSET"
            math OFFSET + BASE_OFF
            log NAME OFFSET SIZE
            math OFFSET = NEXT_OFF
        endif
    next i
endfunction
