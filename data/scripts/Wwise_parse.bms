# file types: Wwise *.txt with same name as *.bnk files or soundbanks.xml
# script type: parser
# note 1: all *.wem files must be in the same folder as this script and the txt
# note 2: script will maintain the original folder structure
#
# (c) by AlphaTwentyThree of Zenhax
# script for QuickBMS http://quickbms.aluigi.org

get EXT extension
if EXT == "txt"
	callfunction bnk_txt 1
elif EXT == "xml"
	callfunction soundbanks 1
else
	print "script only for txt and xml"
endif

startfunction bnk_txt
	callfunction getARRAYSIZE 1
	print "columns: %COLUMNS%, lines: %LINES%, %EMPTY% of which are empty"
	xmath LINES "LINES - EMPTY"
	goto 0
	set ARRAYLINE 0
	for i = 0 <= LINES
		set HEADERLINE 0
		for j = 0 < COLUMNS
			set NEWENTRY 0
			getCT FIELD string 0x09
			#print "(%i%, %j%): %FIELD%"
			if FIELD == "ID" # check for every line in case structure changes
				set C1 j
				set HEADERLINE 1 # don't enter data
			elif FIELD == "Wwise Object Path"
				set C2 j
				set HEADERLINE 1
			endif
			if HEADERLINE == 0
				if j == C1
					putArray 0 ARRAYLINE FIELD
					set NEWENTRY 1
					#print "enter ID %FIELD% at line %ARRAYLINE%"
				elif j == C2
					#print "enter path %FIELD% at line %ARRAYLINE%"
					putArray 1 ARRAYLINE FIELD
					set NEWENTRY 1
				endif
			endif
		next j
		getCT FIELD string 0x0d
		#print "(%i%, %j%): %FIELD%"
		get DUMMY byte
		if NEWENTRY == 1
			math ARRAYLINE += 1
		endif
		#print "line %i%: %NEWENTRY%"
	next i
	#print "entries: %ARRAYLINE%"

	for i = 0 < ARRAYLINE
		getArray IDX 0 i
		getArray NAME 1 i
		string NAME += ".wem"
		string IDX += ".wem"
		open FDSE IDX 1 EXIST
		if EXIST == 1
			get SIZE asize 1
			log NAME 0 SIZE 1
		endif
	next i
endfunction

startfunction getARRAYSIZE
	FindLoc SEARCHLINEEND string \x0d\x0a 0 ""
	getDstring FIRSTLINE SEARCHLINEEND
	strlen LENGTH FIRSTLINE
	set COLUMNS 0
	for i = 0 < LENGTH
		getVarChr TEST FIRSTLINE i byte
		if TEST == 0x09
			math COLUMNS += 1
		endif
	next i
	set LINES 0
	set LINEEND 0
	
	set EMPTY 0
	for
		goto LINEEND
		savepos MYOFF
		FindLoc LINEEND string \x0d\x0a 0 ""
		if LINEEND == ""
			break
		endif
		xmath TEST "LINEEND - MYOFF"
		math LINES += 1
		if TEST == 0
			print "line %LINES% is empty"
			math EMPTY += 1
		endif
		math LINEEND += 2
	next
	goto -4
	get TEST long
	if TEST == 0x0a0d0a0d
		math LINES -= 1
	endif
endfunction

startfunction soundbanks
	for i = 1
		FindLoc SEARCH string "<File Id=" 0 ""
		if SEARCH == ""
			cleanexit
		endif
		goto SEARCH
		getDstring DUMMY 10
		getCT DIDX string 0x22
		string DIDX += ".wem"
		getDstring TEST 3 # sometimes the file id tag is empty
		if TEST != " />"
			math SEARCH += 12
			goto SEARCH
			FindLoc SEARCH string "<Path>" 0 ""
			goto SEARCH
			getDstring DUMMY 6
			getCT FNAME string 0x3c
			string FNAME -= 13
			open FDSE DIDX 1 EXIST
			if EXIST == 1
				get SIZE asize 1
				log MEMORY_FILE 0 0
				log MEMORY_FILE 0 SIZE 1
				string FNAME += ".wem"
				log FNAME 0 SIZE MEMORY_FILE
			endif
		endif
		math SEARCH += 10
		goto SEARCH
	next i
endfunction
