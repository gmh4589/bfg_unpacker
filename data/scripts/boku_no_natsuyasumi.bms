# Boku no Natsuyasumi (PS1) - SCPS_100.88/BOKU.BIN

open FDSE "SCPS_100.88" 0
open FDSE "BOKU.BIN" 1
goto 0x1404c 0
for i = 0 < 0x116
	get OFFSET long
	math OFFSET * 0x800
	putarray 0 i OFFSET
next i
goto 0x14510 0
for i = 0 < 0x116
	get SIZE long
	putarray 1 i SIZE
next i
goto 0x149d4 0
for i = 0 < 0x116
	getarray OFFSET 0 i
	getarray SIZE   1 i
	get NAME_OFF long
	math NAME_OFF - 0x80000800
	math NAME_OFF - 0xf000
	savepos TMP3
	goto NAME_OFF
	get NAME string
	goto TMP3
	
	if i > 2
		if OFFSET >= 0x0020f800
			math OFFSET - 0x0020b000
		endif
		log NAME OFFSET SIZE 1
	endif
next i
