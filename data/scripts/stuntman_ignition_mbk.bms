# Stuntman Ignition (MBK) (work-in-progress)
# script for QuickBMS http://quickbms.aluigi.org

idstring "GKP@"
idstring "MBNK"
get ZERO long
get DUMMY long      # 3 or 1
get INFO_OFF long   # 0x800
get ZERO long
get DUMMY long      # 0x20
goto INFO_OFF

goto 0x850
get FILES long
goto 0x880
get BASE_OFF long
goto 0x840
get NUM long
goto 0x86c
get OFFSET long
math OFFSET + INFO_OFF
goto OFFSET

# long story short, I don't know how to reach the necessary offset, lame archive format
do
    get DUMMY long
while DUMMY u<= 0xf0000000
do
    get DUMMY long
while DUMMY u>= 0xf0000000
goto -4 0 SEEK_CUR
padding 16

get MBK_NAME basename

for i = 0 < FILES
    get INTERLEAVE short
    get CHANNELS short
    get FREQUENCY long
    get XSIZE long
    get ZERO long
    get SIZE long
    get DUMMY long
    get OFFSET long
    if MBK_NAME & "sfxmusic"
        math OFFSET + BASE_OFF
    endif
    set NAME string ""
    if SIZE != 0
        #log NAME OFFSET SIZE
        callfunction DUMP_VAG
    endif
next i

# you must provide the following values: NAME OFFSET SIZE FREQUENCY CHANNELS and INTERLEAVE
startfunction DUMP_VAG
    endian save CURRENT_ENDIAN
    endian little
    putvarchr MEMORY_FILE SIZE 0
    log MEMORY_FILE 0 0
    put 0x64685353 long MEMORY_FILE
    put 0x18 long MEMORY_FILE
    put 0x10 long MEMORY_FILE
    put FREQUENCY long MEMORY_FILE
    put CHANNELS long MEMORY_FILE
    put INTERLEAVE long MEMORY_FILE
    put 0 long MEMORY_FILE
    put 0xffffffff long MEMORY_FILE
    put 0x64625353 long MEMORY_FILE
    put SIZE long MEMORY_FILE
    append
    log MEMORY_FILE OFFSET SIZE
    append

    get SIZE asize MEMORY_FILE
    log NAME 0 SIZE MEMORY_FILE
    if CURRENT_ENDIAN != 0
        endian big
    endif
endfunction
