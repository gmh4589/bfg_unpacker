# extracts the contents of the data.vfs from Crash Bandicoot: Nitro Kart 2 (iP, 2010)
# (c) 2012-12-05 by AlphaTwentyThree of XeNTaX

include "func_getTYPE.bms"
idstring "FUFS"
get UNK long
get FILES long
for i = 1 <= FILES
   get OFFSET long
   get NAME_CRC long
   get SIZE long
   savepos MYOFF
   goto OFFSET
   get TEST byte
   set COMP 0
   if TEST == 0x78
      set COMP 1
      set ZSIZE SIZE
      math ZSIZE *= 0x300
   elif TEST == 0x50
      goto OFFSET
      getDstring TEST 4
      if TEST == "PLZP"
         set COMP 1
         get ZSIZE long
         math ZSIZE *= 0x10
         math OFFSET += 0xc
      endif
   else
      putVarChr MEMORY_FILE SIZE 0
      log MEMORY_FILE 0 0
      append
      log MEMORY_FILE OFFSET SIZE
      append
   endif
   if COMP == 1
      putVarChr MEMORY_FILE ZSIZE 0
      log MEMORY_FILE 0 0
      append
      clog MEMORY_FILE OFFSET SIZE ZSIZE
      append
   endif
   callfunction getTYPE 1
   get NAME basename
   string NAME_CRC p= "_0x%08x" NAME_CRC
   string NAME += NAME_CRC
   string NAME += EXT
   log NAME 0 SIZE MEMORY_FILE
   goto MYOFF
next i