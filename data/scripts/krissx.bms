# KrissX (only files and texts RGLT supported)
# script for QuickBMS http://quickbms.aluigi.org

idstring RGLT
get ARCH_SIZE long
get FIELDS long

set TEXT_MODE long 0
for i = 0 < FIELDS
    getdstring NAME 4
    get SIZE long
    get OFFSET long
    if i == 0   #if NAME == "aHDR"
        set FILES long OFFSET
    elif i == 1 #elif NAME == "aOFF"
        set INFO_OFF long OFFSET
    elif i == 2 #elif NAME == "aFLS"
        set BASE_OFF long OFFSET
    endif
    if NAME == "tOFS"
        set TEXT_MODE long 1
    endif
next i

goto FILES
get FILES long
goto INFO_OFF
for i = 0 < FILES
    if TEXT_MODE == 0
        getdstring NAME 0x80
        get CRC long
        get OFFSET long
        get SIZE long
        math OFFSET += BASE_OFF
        log NAME OFFSET SIZE
    else
        get CRC long
        get DUMMY long
        get OFFSET long
        get SIZE long
        get SIZE long
        math OFFSET += BASE_OFF
        savepos TMP
        goto OFFSET
        getdstring NAME SIZE
        goto TMP
        print "%NAME%"
    endif
next i
