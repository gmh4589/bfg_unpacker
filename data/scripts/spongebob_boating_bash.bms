# SpongeBob's Boating Bash
# script for QuickBMS http://quickbms.aluigi.org

math CHUNK_SIZE = 0x4000
get DUMMY long
for
    getdstring DATA_NAME 4
    get DATA_SIZE long
    savepos DATA_OFFSET

    if DATA_NAME == "RIFF"
        xmath NEXT_DATA_OFFSET "DATA_OFFSET + 0x10"
    else
        xmath NEXT_DATA_OFFSET "DATA_OFFSET + DATA_SIZE"

        # directories are not handled!

        if DATA_NAME == "file"
            get DUMMY long
            get SIZE long

            get CHUNK_OFF long
            putarray 0 0 CHUNK_OFF
            xmath CHUNKS "(CHUNK_OFF - 0xc) / 4"
            for x = 1 <= CHUNKS
                get CHUNK_OFF long
                putarray 0 x CHUNK_OFF
            next x
            math CHUNKS += 1

            putvarchr MEMORY_FILE SIZE 0
            log MEMORY_FILE 0 0
            append
                getarray CHUNK_OFF 0 0
            for x = 1 <= CHUNKS
                if x == CHUNKS
                    math NEXT_CHUNK_OFF = NEXT_DATA_OFFSET
                else
                    getarray NEXT_CHUNK_OFF 0 x
                endif
                xmath CHUNK_ZSIZE "NEXT_CHUNK_OFF - CHUNK_OFF"
                math CHUNK_OFF += DATA_OFFSET
                clog MEMORY_FILE CHUNK_OFF CHUNK_ZSIZE CHUNK_SIZE
                math CHUNK_OFF = NEXT_CHUNK_OFF
            next x
            append
            log "" 0 SIZE MEMORY_FILE
        endif
    endif
    goto NEXT_DATA_OFFSET
    padding 2
next
