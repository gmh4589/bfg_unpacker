# extract archives with BIGF signature from
# WarCraft II: The Dark Saga (Playstation & Saturn)
# (c) 2012-05-17 by AlphaTwentyThree of XeNTaX

idstring "BIGF"
# endian big # un-comment for Saturn version
get UNK long
get FILES long
get FSIZE asize
set i 1
for
   get OFFSET long
   if OFFSET == 0xadf0adf0
      cleanexit
   elif OFFSET == 0xf0adf0ad
      cleanexit
   endif
   get SIZE long
   set TEST SIZE
   math TEST += OFFSET
   if TEST > FSIZE # corrupt file end
      print "Warning: Last file may be truncated"
      get SIZE asize
      math SIZE -= OFFSET
   endif
   if SIZE != 0xffffffff
      get NAME basename
      string NAME += "_"
      string NAME += i
      savepos MYOFF
      goto OFFSET
      getDstring TYPE 4
      if TYPE == "BANK"
         string NAME += ".bank"
      endif
      goto MYOFF
      log NAME OFFSET SIZE
      math i += 1
   endif
next