# Culdcept Expansion Plus
# script for QuickBMS http://quickbms.aluigi.org

get NAME basename
if NAME & "_unchunked"
else
    print "Error: you must first use this script on the original DAT/MOV file:\n  http://aluigi.org/bms/ps1_disk_chunks.bms"
    cleanexit
endif

get EXT extension
if EXT == "mov"

    get ZERO long
    for i = 0
        get OFFSET long
        if OFFSET == 0
            break
        endif
        math OFFSET * 0x800
        putarray 0 i OFFSET
    next i
        get OFFSET asize
        putarray 0 i OFFSET
    math FILES = i

    sortarray 0
    for i = 0 < FILES
        getarray OFFSET 0 i
        math i + 1
        getarray SIZE   0 i
        math SIZE - OFFSET
        if SIZE != 0
            log "" OFFSET SIZE
        endif
    next

else

    math FILES = -1
    for i = 0 != FILES
        get OFFSET long
        get SIZE long
        if i == 0
            math FILES = OFFSET
            math FILES / 8
        endif
        log "" OFFSET SIZE
    next i

endif
