# extract sounds from *.snd/*.shd pairs (CRI engine)
#
# supported games:
# - Arasuji de Kitaeru Hayamimi no Susume DS
#
# (c) 2012-03-19 by AlphaTwentyThree of XeNTaX

include "func_getTYPE.bms"
open FDDE SHD 0 EXISTS
open FDDE SND 1
if EXISTS == 1 # diff qualities
   set QUALPOS 0
   set INFOPOS 0x20
   for i = 1 <= 4
      log MEMORY_FILE 0 0
      goto QUALPOS 0
      get OFFSET long   0
      get SIZE long 0
      savepos QUALPOS 0
      log MEMORY_FILE2 OFFSET SIZE 1
      goto INFOPOS 0
      get STARTPOS long 0
      get FILES long 0
      math FILES /= 8
      savepos INFOPOS 0
      math STARTPOS += 0x20
      goto STARTPOS 0
      
      for j = 1 <= FILES
         get OFFSET long 0
         get SIZE long 0
         log MEMORY_FILE OFFSET SIZE MEMORY_FILE2
         callfunction getname 1
         callfunction getTYPE 1
         string NAME += EXT
         log NAME 0 SIZE MEMORY_FILE
      next j
   next i
else
   open FDDE SND 0
   get FILES long
   math FILES /= 8
   goto 0
   for i = 1 <= FILES
      get OFFSET long
      get SIZE long
      log MEMORY_FILE OFFSET SIZE
      callfunction getTYPE 1
      get NAME basename
      string NAME += "_"
      string NAME += i
      string NAME += EXT
      log NAME 0 SIZE MEMORY_FILE
   next i
endif

startfunction getname
   get NAME basename 0
   string NAME += "_"
   string NAME += i
   string NAME += "_"
   string NAME += j
endfunction