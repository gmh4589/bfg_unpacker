# extract the *.sdu and *.vdu files from The Suffering (Xbox)
# (c) 2012-05-29 by AlphaTwentyThree of XeNTaX

include "func_header_XADP.bms" # be sure to have this file in the same directory!
idstring "SRSC"
get UNK byte
get UNK byte
get INFO long
get ENTRIES long # head/file/tail entries
goto INFO
get EXT extension
if EXT == "sdu"
   callfunction sdu 1
elif EXT == "vdu"
   callfunction vdu
endif


startfunction sdu
   for i = 1 <= ENTRIES
      get SIG byte # head/data/tail
      get UNK2 byte #always 3?
      get FILE_ID short
      get UNK4 short # some identifier
      get OFFSET long
      get SIZE long
      if SIG == 2
         if FILE_ID != 0
            savepos MYOFF
            math OFFSET += 0x2c
            goto OFFSET
            get CH short
            get BITS short # always 4
            get FREQ long
            goto MYOFF
         endif
      elif SIG == 4
         get NAME basename
         string NAME += "_"
         string NAME += FILE_ID
         #string NAME += "_"
         #string NAME += UNK4
         string NAME += ".lwav"
         callfunction XADP 1
      elif SIG == 5 # some file tail - do nothing?
      endif
   next i
endfunction

startfunction vdu
   for i = 1 <= ENTRIES
      get SIG byte # head/data/tail
      get UNK2 byte #always 3?
      get FILE_ID short
      get UNK4 short # some identifier
      get OFFSET long
      get SIZE long
      if SIG != 66
         get NAME basename
         string NAME += "_"
         string NAME += FILE_ID
         string NAME += ".xmv"
         if FILE_ID != 0
            log NAME OFFSET SIZE
         endif
      endif
   next i
endfunction