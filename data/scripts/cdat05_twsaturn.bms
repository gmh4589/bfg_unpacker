# Crystal Dynamics Archive Type 5(BIGFILE.FOO/BIGFILE/TITAN.CLS)
# ---
# Solar Eclipse/Titan Wars (SATURN)
# 3D Baseball (PS1, SATURN)

set NAME string ""
set PATH string ""
math OFFSET = 0
callfunction EXTRACT 1

startfunction EXTRACT
	string PATH + NAME
	string PATH + /
	goto OFFSET
	savepos BASE_OFF
	get FILES long
	endian guess FILES
	get ZERO long
	get DUMMY long
	xmath HASH_OFF "BASE_OFF + FILES * 12"
	math FILES - 1
	savepos TMP1
	goto HASH_OFF
	get HASH1 long
	for i = 0 < FILES
		get HASH2 long
		putarray 0 i HASH2
	next i
	goto TMP1
	for i = 0 < FILES
		getarray HASH2 0 i
		get SIZE long
		get OFFSET long
		get TYPE long
		math OFFSET * 0x800
		math OFFSET + BASE_OFF
		string NAME p "%08x" HASH2
		if TYPE == 3
			savepos TMP2
			callfunction EXTRACT
			goto TMP2
		else
			string FNAME p "%08x/%s%s." HASH1 PATH NAME
			log FNAME OFFSET SIZE
		endif
	next i
endfunction
