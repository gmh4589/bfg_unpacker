# Tekken Tag Tournament 2
#   ENCRYPTED FORMAT, DON'T USE!
# script for QuickBMS http://quickbms.aluigi.org

endian big

open FDDE "OFS"
callfunction MP4_CHUNKS 1
for CHUNK = 0 < CHUNKS
    getarray CHUNK_TYPE 0 CHUNK
    if CHUNK_TYPE == "ofsd"
        getarray CHUNK_OFF  1 CHUNK
        getarray CHUNK_SIZE 2 CHUNK
        goto CHUNK_OFF
        xmath FILES "CHUNK_SIZE / 4"
        for i = 0 < FILES
            get OFFSET long
            putarray 10 i OFFSET
        next i
    endif
next CHUNK

open FDDE "BIN"
callfunction MP4_CHUNKS 1
for CHUNK = 0 < CHUNKS
    getarray CHUNK_TYPE 0 CHUNK
    if CHUNK_TYPE == "imge"
        getarray CHUNK_OFF  1 CHUNK
        getarray CHUNK_SIZE 2 CHUNK
            putarray 10 i CHUNK_SIZE    # in case the last one is not already saved (it is)
        for i = 0 < FILES
            getarray OFFSET 10 i
            math i + 1
            getarray SIZE   10 i
            math SIZE - OFFSET
            math OFFSET + CHUNK_OFF
            if SIZE != 0
                log "" OFFSET SIZE
            endif
        next
    endif
next CHUNK

startfunction MP4_CHUNKS
    get CHUNK_LIMIT asize
    math CHUNK = 0
    for CHUNK_OFF = 0 != CHUNK_LIMIT
        get CHUNK_SIZE long
        math CHUNK_SIZE - 8
        getdstring CHUNK_TYPE 4
        savepos CHUNK_OFF
        putarray 0 CHUNK CHUNK_TYPE
        putarray 1 CHUNK CHUNK_OFF
        putarray 2 CHUNK CHUNK_SIZE
        math CHUNK_OFF + CHUNK_SIZE
        goto CHUNK_OFF
    next CHUNK
    math CHUNKS = CHUNK
endfunction
