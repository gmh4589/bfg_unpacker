# Luna Moonlight / Dragona / Scarlet Blade GFPK (script 0.2.2)
# script for QuickBMS http://quickbms.aluigi.org

# you must choose the correct key!
set KEY binary "xlaqj#em$%^fk@#$rh!@#sk@#$!"  # Dragona
set KEY binary "\x73\x64\x66\x61\x73\x6C\x64\x66\x6B\x6A\x66\x73\x6C\x6B\x65\xA4\xA9\xA4\xB8\xA4\xA7\xA4\xA9\xA4\xA9\xA4\xA4\xA4\xB7\x73\x64\x66\x21\x40\x40\x23\x21\x40\x46\x24\x47\x67\x66\x67\x64\x73"   # Scarlet Blade ("sdfasldfkjfslke¤©¤¸¤§¤©¤©¤¤¤·sdf!@@#!@F$Ggfgds")

get VER float   # 1.5
getdstring NAME 0x100
get FILES long
if FILES & 0x80000000   # not sure, apparently even if not & 0x80000000 some Dragona archives are encrypted
    strlen KEYSZ KEY
    math KEYSZ + 1  # the final NULL byte is necessary!
    set XOR_KEY string ""
    for x = 0 < KEYSZ
        getvarchr TMP2 KEY x
        string TMP2 p " 0x%02x" TMP2
        string XOR_KEY + TMP2
    next x
    savepos TMP_OFF
    filexor XOR_KEY TMP_OFF
endif
math FILES & 0x7fffffff

for i = 0 < FILES
    get DUMMY long
next i
for i = 0 < FILES
    get OFFSET long
    putarray 0 i OFFSET
next i
for i = 0 < FILES
    getdstring NAME 0xf8
    get DUMMY long
    get DUMMY long
    get DUMMY long
    getdstring NAME 0x204
    getdstring PADDING 8

    string TMP p "%s" NAME  # bypass a bug in quickbms!
    putarray 1 i TMP
next i
filexor ""

sortarray 0 1

savepos BASE_OFF
math OFFSET = 0
for i = 0 <= FILES
    if i == FILES
        get NEXT_OFFSET asize
        math NEXT_OFFSET - BASE_OFF
    else
        getarray NEXT_OFFSET 0 i
    endif
    if NEXT_OFFSET >= 0
        if i != 0
            xmath SIZE "NEXT_OFFSET - OFFSET"
            if SIZE > 0
                math OFFSET + BASE_OFF
                log NAME OFFSET SIZE
            endif
        endif
        if i < FILES
            getarray NAME 1 i
        endif
        math OFFSET = NEXT_OFFSET
    endif
next i
