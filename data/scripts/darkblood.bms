# Dark Blood Online (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

#idstring "P\0P\0K\0\0"
goto 0x218
get HEAD_SIZE long
savepos BASE_OFF
math BASE_OFF += HEAD_SIZE
get FILES long
get DATA_SIZE long
get DUMMY byte
get DUMMY short
get DUMMY long
for i = 0 < FILES
    callfunction GET_UGLYSTRING 1
    set NAME string TMPNAME
    callfunction GET_UGLYSTRING 1
    get CRC long
    get ZSIZE long
    get SIZE long
    get OFFSET long
    get DUMMY byte
    math OFFSET += BASE_OFF

    set EXT extension NAME
    if EXT == "j"
        string NAME -= 2
        savepos TMP_OFF
        goto OFFSET
        callfunction GET_UGLYNUM 1
        math ZSIZE = NUM
        get ZIP byte
        if ZIP == 0
            savepos OFFSET
            log NAME OFFSET ZSIZE
        else
            math ZSIZE -= 4
            get SIZE long
            savepos OFFSET
            clog NAME OFFSET ZSIZE SIZE
        endif
        goto TMP_OFF
    else
        log NAME OFFSET SIZE
    endif
next i

startfunction GET_UGLYNUM
    math NUM = 0
    math x = 0
    do
        get T byte
        math TMP = T
        math TMP &= 0x7f
        math TMP <<= x
        math NUM += TMP
        math x += 7
    while T <= 0x7f
endfunction

startfunction GET_UGLYSTRING
    callfunction GET_UGLYNUM 1
    math NAMESZ = NUM
    getdstring TMPNAME NAMESZ
    set TMPNAME unicode TMPNAME
endfunction
