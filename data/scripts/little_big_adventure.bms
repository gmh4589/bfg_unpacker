# Little Big Adventure (script 0.2.1)
# script for QuickBMS http://quickbms.aluigi.org

quickbmsver "0.7.7"
get HQR_SIZE asize
for
    get OFFSET long
    if OFFSET == HQR_SIZE
        break
    endif
    if OFFSET != 0
        savepos TMP
        goto OFFSET
        get SIZE long
        get ZSIZE long
        get ZIP short
        savepos OFFSET
        if ZIP == 0
            log "" OFFSET SIZE
        elif ZIP == 1
            comtype lbalzss1
            clog "" OFFSET ZSIZE SIZE
        elif ZIP == 2
            comtype lbalzss2
            clog "" OFFSET ZSIZE SIZE
        else
            print "Error: unknown compression algorithm %ZIP%"
            cleanexit
        endif
        goto TMP
    endif
next
