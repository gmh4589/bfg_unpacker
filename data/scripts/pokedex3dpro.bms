# Pokemon Pokedex 3D Pro
# script for QuickBMS http://quickbms.aluigi.org

get FILES long
get DUMMY long
get DUMMY long
get DUMMY long
get DUMMY long
get DUMMY long
math FILES - 1
for i = 0 < FILES
    get DUMMY long
    get DUMMY long
    get DUMMY long
    get OFFSET long
    get DUMMY long
    get DUMMY long
    get SIZE long
    get DUMMY long
    callfunction DO_ZIP # 1
next i

startfunction DO_ZIP
    savepos BCK_OFF
    goto OFFSET
    math zip_limit = OFFSET
    math zip_limit + SIZE
    for offset = offset u< zip_limit
        idstring "PK"
        get sign short
        if sign != 0x0403
            break
        endif

        get ver             short
        get flag            short
        get method          short
        get modtime         short
        get moddate         short
        get crc             long
        get comp_size       long
        get uncomp_size     long
        get name_len        short
        get extra_len       short
        getdstring name     name_len
        getdstring extra    extra_len
        savepos offset

        if method == 0
            Log name offset comp_size   # was uncomp_size before AES
        else
            if method == 8
                ComType deflate
            elif method == 14
                ComType lzmaefs
            elif method == 98
                ComType ppmd
            else
                print "unsupported compression method %method%"
                cleanexit
            endif
            CLog name offset comp_size uncomp_size
        endif

        math offset += comp_size
        goto offset
    next
    goto BCK_OFF
endfunction
