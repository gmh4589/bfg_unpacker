# Rainbow Six Lockdown PS2DATA
#   Notes: TXT are probably encrypted, the ADP have an unknown 0x80000000 flag, PS2 are FSB3
# script for QuickBMS http://quickbms.aluigi.org

open FDDE "bin"
get NAMESZ long
getdstring NAME NAMESZ

get NAMESZ long
getdstring NAME NAMESZ
get FOLDERS long

for i = 0 < FOLDERS
    get NAMESZ long
    getdstring NAME NAMESZ
    putarray 0 i NAME
next i
get FILES long
for i = 0 < FILES
    get FOLDER_ID byte
    get NAMESZ long
    getdstring NAME NAMESZ
    getarray PATH 0 FOLDER_ID
    string NAME p "%s%s" PATH NAME
    putarray 1 i NAME
next i

get FILES long
for i = 0 < FILES
    get SIZE long
    putarray 2 i SIZE
next i

get FILES long
for i = 0 < FILES
    get OFFSET long
    putarray 3 i OFFSET
next i

putarray 1 FILES ""
math PCK_NUM = -1
for i = 0 < FILES
    getarray NAME   1 i
    getarray SIZE   2 i
    getarray OFFSET 3 i
    if OFFSET == 0
        math PCK_NUM + 1
        if PCK_NUM == 0
            string TMP p "PAK"
        else
            string TMP p "PA%d" PCK_NUM
        endif
        open FDDE TMP
    endif
    if SIZE & 0x80000000    # ???
        math SIZE & 0x7fffffff
        log NAME OFFSET SIZE
    else
        log NAME OFFSET SIZE
    endif
next i
