# RedLynx Trials 2 Second Edition / HD (script 0.1.3)
# script for QuickBMS http://quickbms.aluigi.org

idstring "\x78\x56\x34\x12"
get OFFSET long
get FILES long
savepos INFO_OFF

# work-around, lame
for i = 0 < FILES
    get DUMMY long
    get ZSIZE long
    get SIZE long
    get ZIP byte
    get OFFSET long
    savepos TMP
    if ZIP == 0
        goto OFFSET
        get XFILES long
        math XFILES += 1
        if XFILES == FILES
            math i = FILES  # break doesn't work
        endif
        goto TMP
    endif
next i
if XFILES == FILES
    log MEMORY_FILE OFFSET SIZE
    get FILES long MEMORY_FILE  # the original FILES includes also the NAME entry
else
    math TMP = FILES
    math TMP *= 2
    putvarchr MEMORY_FILE TMP 0
endif

goto INFO_OFF
for i = 0 < FILES
    get DUMMY long
    get ZSIZE long
    get SIZE long
    get ZIP byte
    get OFFSET long
    get NAMESZ short MEMORY_FILE
    getdstring NAME NAMESZ MEMORY_FILE

    if ZIP == 0
        log NAME OFFSET SIZE
    elif ZIP == 4
        comtype xmemdecompress
        savepos TMP
        goto OFFSET
        get SIZE long
        get ZSIZE long
        savepos OFFSET
        clog NAME OFFSET ZSIZE SIZE
        goto TMP
    else
        comtype zlib
        clog NAME OFFSET ZSIZE SIZE
    endif
next i
