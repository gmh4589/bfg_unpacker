# extracts the all_#.bundle files from Payday 2 (PS3)
# written by AlphaTwentyThree 2014-10-09
# script for QuickBMS http://aluigi.org/papers.htm#quickbms

include "func_getTYPE.bms"
get NAME basename 0
string NAME p= "%s_h.bundle" NAME
open FDSE NAME 1 EXIST
if EXIST == 0
   print "Error: be sure you ust this script on an all_#.bundle file! Aborting."
   cleanexit
endif
get FSIZE asize 1
endian big
get DUMMY long 1
get CSIZE long 1 # complete decompressed size
log MEMORY_FILE 0 0
putVarChr MEMORY_FILE2 CSIZE 0
log MEMORY_FILE2 0 0
savepos OFFSET 1
callfunction decompress 1
goto 0 MEMORY_FILE2
get SIZE_TOC long MEMORY_FILE2
get CRC long MEMORY_FILE2
get FILES long MEMORY_FILE2
get FILES long MEMORY_FILE2
get UNK long MEMORY_FILE2
get UNK long MEMORY_FILE2
get UNK long MEMORY_FILE2
get BNAME basename 0
for i = 1 <= FILES
   get FID long MEMORY_FILE2
   get OFFSET long MEMORY_FILE2
   get SIZE long MEMORY_FILE2
   if SIZE != 0
      log MEMORY_FILE OFFSET SIZE 0
      callfunction getTYPE 1
      endian big
      string NAME p= "%s/%s_%d%s" BNAME BNAME FID EXT
      log NAME 0 SIZE MEMORY_FILE
   endif
next i


startfunction decompress
   append
   do
      goto OFFSET 1
      get SIZE long 1
      savepos OFFSET 1
      xmath ZSIZE "SIZE * 100"
      clog MEMORY_FILE2 OFFSET SIZE ZSIZE 1
      math OFFSET += SIZE
   while OFFSET < FSIZE
   append
endfunction