# Riptide GP2 / Vector Unit games (script 0.1.3)
# script for QuickBMS http://quickbms.aluigi.org

idstring "FPUV"
get VER long    # 5
get OFFSET long
get FILES long
goto OFFSET
for i = 0 < FILES
    get NAME string
    get OFFSET long
    get SIZE long
    get ZSIZE long
    get CRC long
    get DUMMY short
    get ZIP short
    string NAME + .
    if ZIP == 0
        savepos TMP
        goto OFFSET
        get CHECK long
        goto TMP
        math CHECK + 4
        if CHECK == SIZE
            math OFFSET + 4
            math SIZE   - 4
        endif
        log NAME OFFSET SIZE
    else
        if ZIP == 1
            comtype zlib
        elif ZIP == 2
            comtype lzma
        elif ZIP == 3
            comtype snappy
        elif ZIP == 4
            comtype lz4
        elif ZIP == 5
            comtype lz4
        else
            print "Error: unknown compression algorithm %ZIP%, contact me"
            cleanexit
        endif
        clog NAME OFFSET ZSIZE SIZE
    endif
next i
