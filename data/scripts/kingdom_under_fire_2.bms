# Kingdom Under Fire 2 (script 0.2)
# script for QuickBMS http://quickbms.aluigi.org

get dwSeed asize
idstring "BEAR"
get VER long    # 16bits.16bits
get FILES longlong

for i = 0 < FILES
    get SOME_CRC longlong
    get ZSIZE long
    get SIZE long
    get ID long
    get ZERO long

    callfunction CRYPT_INC 1
    math SOME_CRC + dwSeed
    callfunction CRYPT_INC 1
    math ZSIZE + dwSeed
    callfunction CRYPT_INC 1
    math SIZE + dwSeed
    callfunction CRYPT_INC 1
    math ID + dwSeed
    callfunction CRYPT_INC 1
    math ZERO + dwSeed

    putarray 0 i SOME_CRC
    putarray 1 i ZSIZE
    putarray 2 i SIZE
    putarray 3 i ID
    putarray 4 i ZERO
next i

for i = 0 < FILES
    getarray ZSIZE 1 i
    getarray SIZE  2 i

    putvarchr MEMORY_FILE SIZE 0
    log MEMORY_FILE 0 0
    append
    for MEM_SIZE = 0 != SIZE
        get CHUNK_OFF long
        get CHUNK_ZSIZE long
        get CHUNK_SIZE long

        callfunction CRYPT_INC 1
        math CHUNK_OFF + dwSeed
        callfunction CRYPT_INC 1
        math CHUNK_ZSIZE + dwSeed
        callfunction CRYPT_INC 1
        math CHUNK_SIZE + dwSeed

        if CHUNK_ZSIZE == 0
            log MEMORY_FILE CHUNK_OFF CHUNK_SIZE
        else
            if CHUNK_SIZE == 0
                math CHUNK_SIZE = 0x10000
            endif
            clog MEMORY_FILE CHUNK_OFF CHUNK_ZSIZE CHUNK_SIZE
        endif
    next MEM_SIZE + CHUNK_SIZE
    append
    log "" 0 SIZE MEMORY_FILE
next i

# thanks Ekey https://zenhax.com/viewtopic.php?p=33563#p33563
startfunction CRYPT_INC
    if VER >= 0x00020001
        math dwSeed * 0x19660D
        math dwSeed + 0x3C6EF35F
    endif
endfunction

