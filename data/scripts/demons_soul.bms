# Demon's Soul (script 0.1.3)
# script for QuickBMS http://quickbms.aluigi.org

endian big
comtype deflate

get EXT extension
if EXT == "dcx"
    # unpack the DCX archive so you can reimport the files if you want
    idstring "DCX"

    goto 0x68   # only for guessing endian
    get CHUNKS long
    endian guess CHUNKS

    goto 0x1c
    get FILE_SIZE long
    get FILE_ZSIZE long
    goto 0x68
    get CHUNKS long
    get DUMMY long
    log MEMORY_FILE 0 0
    savepos BASE_OFF
    xmath BASE_OFF "BASE_OFF + (CHUNKS * 0x10)"
    append
    for i = 0 < CHUNKS
        get ZERO long
        get OFFSET long
        get ZSIZE long
        get ZIP long
        math OFFSET + BASE_OFF
        if ZIP == 0
            log MEMORY_FILE OFFSET ZSIZE
        else
            clog MEMORY_FILE OFFSET ZSIZE 0x10000
        endif
    next i
    append
    get NAME basename
    log NAME 0 FILE_SIZE MEMORY_FILE
    cleanexit
endif


# the extension is .*bnd

idstring "BND"  # BND307D7R6

goto 16 # only for guessing endian
get FILES long
endian guess FILES

goto 12
get FLAGS long  # FLAGS with endianess? (0x2e010100 or 0x00000074)
get FILES long
get BASE_OFF long
get ZERO long
get ZERO long
math NAME_OFF = 0
for i = 0 < FILES
    get DUMMY long
    get SIZE long
    get OFFSET long
    if FLAGS & 0x0e000000
        get ID long
        get NAME_OFF long
    endif
    if FLAGS & 0x20000000
        get SIZE2 long  # SIZE and SIZE2 are ever the same?
    endif

    if BASE_OFF == 0
        get ZERO long
        get NAME_OFF long
        get XSIZE long
    endif

    if NAME_OFF == 0
        set NAME string ""
    else
        savepos TMP
        goto NAME_OFF
        get NAME string
        goto TMP
    endif

    log NAME OFFSET SIZE
next i
