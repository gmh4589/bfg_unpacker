# Warrior Kings: Battles (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

idstring "PAK File 2.01 (c) Black Cactus Games Limited"
goto 0x30
get OFFSET long
goto OFFSET
get FILES long
for i = 0 < FILES
    get OFFSET long
    if OFFSET == 0xffffffff
        math OFFSET = 0
    endif
    get DUMMY long
    get SIZE long
    getdstring DUMMY 8
    putarray 0 i OFFSET
    putarray 1 i SIZE
next i
get NAMES long
for i = 0 < NAMES
    get IDX long
    getdstring DUMMY 8
    callfunction GET_NAME 1
    putarray 2 IDX NAME
next i

get ARCHIVE_SIZE asize
set paramString string ""
for
    savepos TMP
    if TMP u>= ARCHIVE_SIZE
        break
    endif
    callfunction readDirectory
next

for i = 0 < FILES
    getarray OFFSET 0 i
    getarray SIZE   1 i
    getarray NAME   2 i
    log NAME OFFSET SIZE
next i

startfunction readDirectory
    get i long
    callfunction GET_NAME 1
    if NAMESZ == 0
        break   # necessary to work with some archives without additional tests
    endif
    set str1 string NAME
    get k long
    for x = 0 < k
        get IDX long
        getdstring DUMMY 8
        callfunction GET_NAME 1
        set str2 string NAME
        string NAME p "%s%s/%s" paramString str1 str2
        putarray 2 IDX NAME
    next x
    string paramString + str1
    string paramString + /
    for x = 0 < i
        callfunction readDirectory
    next x
endfunction

startfunction GET_NAME
    get NAMESZ byte
    math NAMESZ * 2
    getdstring NAME NAMESZ
    set NAME unicode NAME
endfunction
