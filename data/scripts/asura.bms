# Asura engine (script 0.2.9)
#  Sniper Elite 1/2/3/4
#  Rogue Trooper
#  Guard Shield
#  NeverDead
#  possibly other Rebellion's games
#   thanx to Terry Butler for having solved the mistery of the filenames in ASTS
# script for QuickBMS http://quickbms.aluigi.org

quickbmsver "0.7.1"

get PACK_SIZE asize

math BASE_OFF = 0
math CHUNKS = 0
math ZIP = 0
getdstring SIGN 8
if SIGN == "AsuraCmp"
    math ZIP = 1
    comtype huffboh
    math OFFSET = 8
elif SIGN == "AsuraZlb"
    math ZIP = 1
    comtype zlib
    math OFFSET = 0x14
    get ZERO long
elif SIGN == "AsuraZbb"
    math CHUNKS = 1
    math ZIP = 1
    comtype zlib
    get FULL_ZSIZE long
    get FULL_SIZE long
endif

if CHUNKS != 0
    log TEMPORARY_FILE 0 0
    append
    log TEMPORARY_FILE 0 10
    log TEMPORARY_FILE 0 10
endif
math CURRENT_SIZE = 0
if ZIP != 0
    do
        savepos TMP
        get ZSIZE long
        if ZSIZE u> PACK_SIZE
            endian big
            reverselong ZSIZE ZSIZE
        endif
        get SIZE long
        if CHUNKS == 0
            math ZSIZE = PACK_SIZE
            math ZSIZE -= OFFSET
        else
            savepos OFFSET
        endif
        callfunction UNPACK_TO_TEMPFILE 1
        math OFFSET + ZSIZE
        goto OFFSET
        math CURRENT_SIZE + SIZE
        if CHUNKS != 0
            if CURRENT_SIZE == FULL_SIZE
                math CHUNKS = 0
                append
                open "." TEMPORARY_FILE
                math BASE_OFF = 0x14
                goto BASE_OFF
                getdstring SIGN 8
            endif
        endif
    while CHUNKS != 0
endif

get PACK_SIZE asize # necessary for the new opened file

if SIGN != "Asura   "
    set XCOMPRESS_SIGN1 binary "\x0f\xf5\x12\xed"
    set XCOMPRESS_SIGN2 binary "\x0f\xf5\x12\xee"
    goto 0
    getdstring SIGN 4
    if SIGN != XCOMPRESS_SIGN1
    if SIGN != XCOMPRESS_SIGN2
        print "Error: unknown type of file %SIGN%"
        cleanexit
    endif
    endif

    endian big
    get ZSIZE asize
    math SIZE = ZSIZE
    math SIZE * 5   # automatically calculated by quickbms
    comtype xmemdecompress
    math OFFSET = 0
    math CHUNKS = 0
    callfunction UNPACK_TO_TEMPFILE 1
endif

get PACK_SIZE asize # necessary for the new opened file

### ENDIAN GUESSSER ###

# doesn't work correctly
#goto 0x10
#get DUMMY short
#if DUMMY == 0
#    endian big
#endif

# should work most of the times
# in case of problems you must specify the endianess manually, example
#   endian big

math TMP = 0x08
math TMP + BASE_OFF
goto TMP
savepos OFFSET
get CHUNK long
get CHUNKSZ long
math OFFSET += CHUNKSZ
if OFFSET u> PACK_SIZE
    endian big
# don't add other checks or you must add also AsuraCmp/Zlb
endif
goto BASE_OFF
getdstring SIGN 8

for x = 0
    savepos CHUNK_OFFSET
    math TMP = CHUNK_OFFSET
    math TMP += 16
    if TMP u>= PACK_SIZE
        break
    endif
    get CHUNK long
    string CHUNK = CHUNK
    get CHUNKSZ long
    get CHUNK_VER long  # ???
    get DUMMY long

    if CHUNK == "RSCF"
        get TYPE long
        get DUMMY long
        get SIZE long
        get NAME string
        math OFFSET = CHUNK_OFFSET
        math OFFSET += CHUNKSZ
        math OFFSET -= SIZE
        log NAME OFFSET SIZE
    elif CHUNK == "ASTS"
        get FILES long

        math NOT_ARCHIVED = 0

        # Sniper Elite 3
        if CHUNK_VER >= 2
            savepos TMP
            get NOT_ARCHIVED byte    # 0 or 1, 1 probably means that the files are not in the archive
            if NOT_ARCHIVED != 0
            if NOT_ARCHIVED != 1
                math NOT_ARCHIVED = 0   # in this case it's part of the name, it's risky with names <= 3 chars
                goto TMP
            endif
            endif
        endif

        for i = 0 < FILES
            callfunction padded_name 1
            get DUMMY byte
            get SIZE long
            if CHUNK_VER == 0
                savepos OFFSET
            else
                get OFFSET long
            endif
            if NOT_ARCHIVED == 0
                log NAME OFFSET SIZE
            endif
            if CHUNK_VER == 0
                math OFFSET + SIZE
                goto OFFSET
            endif
        next i
    else
        savepos OFFSET
        math SIZE = CHUNK_OFFSET
        math SIZE += CHUNKSZ
        math SIZE -= OFFSET
        set NAME string CHUNK
        string NAME += "_chunk/."
        string NAME += x
        string NAME += ".dat"
        log NAME OFFSET SIZE    # comment here if you don't want to extract these useless chunks
    endif
    math CHUNK_OFFSET += CHUNKSZ
    goto CHUNK_OFFSET
next x

startfunction padded_name
    set NAME string ""
    set NAME_POS long 0
    do
        get DUMMY long
        string NAME += "PADD"
        putvarchr NAME NAME_POS DUMMY long
        math NAME_POS += 4
        strlen NAMESZ NAME
    while NAMESZ >= NAME_POS
endfunction

startfunction UNPACK_TO_TEMPFILE
    clog TEMPORARY_FILE OFFSET ZSIZE SIZE
    if CHUNKS == 0
        open "." TEMPORARY_FILE
        get PACK_SIZE asize
        getdstring SIGN 8
    endif
endfunction
