# JX3 Mobile (script 0.1.2)
#   the directory tree of archives with patch and invalid may be wrong
# script for QuickBMS http://quickbms.aluigi.org

comtype lzo1x
set KEY string "\x00\x01\x02\x03\x04\x05\x06\x07\x08\x09\x0a\x0b\x0c\x0d\x0e\x0f\x10\x11\x12\x13\x14\x15\x16\x17\x18\x19\x1a\x1b\x1c\x1d\x1e\x1f\x20\x21\x22\x23\x24\x25\x26\x27\x28\x29\x2a\x2b\x2c\x2d\x2e\x2f\x30\x31\x32\x33\x34\x35\x36\x37\x38\x39\x3a\x3b\x3c\x3d\x3e\x3f\x40\x41\x42\x43\x44\x45\x46\x47\x48\x49\x4a\x4b\x4c\x4d\x4e\x4f\x50\x51\x52\x53\x54\x55\x56\x57\x58\x59\x5a\x5b\x5c\x5d\x5e\x5f\x60\x61\x62\x63\x64\x65\x66\x67\x68\x69\x6a\x6b\x6c\x6d\x6e\x6f\x70\x71\x72\x73\x74\x75\x76\x77\x78\x79\x7a\x7b\x7c\x7d\x7e\x7f\x80\x81\x82\x83\x84\x85\x86\x87\x88\x89\x8a\x8b\x8c\x8d\x8e\x8f\x90\x91\x92\x93\x94\x95\x96\x97\x98\x99\x9a\x9b\x9c\x9d\x9e\x9f\xa0\xa1\xa2\xa3\xa4\xa5\xa6\xa7\xa8\xa9\xaa\xab\xac\xad\xae\xaf\xb0\xb1\xb2\xb3\xb4\xb5\xb6\xb7\xb8\xb9\xba\xbb\xbc\xbd\xbe\xbf\xc0\xc1\xc2\xc3\xc4\xc5\xc6\xc7\xc8\xc9\xca\xcb\xcc\xcd\xce\xcf\xd0\xd1\xd2\xd3\xd4\xd5\xd6\xd7\xd8\xd9\xda\xdb\xdc\xdd\xde\xdf\xe0\xe1\xe2\xe3\xe4\xe5\xe6\xe7\xe8\xe9\xea\xeb\xec\xed\xee\xef\xf0\xf1\xf2\xf3\xf4\xf5\xf6\xf7\xf8\xf9\xfa\xfb\xfc\xfd\xfe\xff"

get INDEX_NAME basename
if INDEX_NAME & "pack"
    set INDEX_NAME string "pack"
elif INDEX_NAME & "data"
    set INDEX_NAME string "index"
endif

string TMP p "%s.idx" INDEX_NAME
callfunction OPEN_FILE 1

filexor KEY
get ZERO long
get FILES long
get FILES_PATCH long
for i = 0 < FILES
    callfunction GET_INFO 1
next i
for i = 0 < FILES_PATCH
    callfunction GET_INFO 1
next i

set PATH string ""
set NAME string ""
savepos TMP
get ENTRY_SIZE asize
math ENTRY_SIZE - TMP
callfunction GET_TREE

for i = 0 < FILES_PATCH
    getarray NAME 5 i
    putarray 4 -1 NAME
next i

math i = 0
set EXT string "dat"
callfunction EXTRACT 1

math FILES + FILES_PATCH
set EXT string "patch"
callfunction EXTRACT 1

startfunction EXTRACT
    goto 0  # necessary for resetting filexor (quickbms bug?)
    if INDEX_NAME & "pack"
        if EXT == "patch"
            string TMP p "%s.%s"  INDEX_NAME EXT
        else
            string TMP p "%s0.%s" INDEX_NAME EXT
        endif
    elif INDEX_NAME == "index"
        string TMP p "data.%s" EXT
    else
        string TMP p "%s.%s" INDEX_NAME EXT
    endif
    callfunction OPEN_FILE 1

    filexor KEY
    for i = i < FILES
        getarray OFFSET 0 i
        getarray FLAGS  1 i
        getarray ZSIZE  2 i
        getarray SIZE   3 i
        getarray NAME   4 i
        if FLAGS & 0x100    # apparently only the patch files have this flag
            # yeah ZSIZE is fake! they are compiled lua files of size SIZE, ZSIZE doesn't match anything
            log NAME OFFSET SIZE
        else
            log NAME OFFSET SIZE
        endif
    next i
endfunction

startfunction GET_TREE
    string PATH + NAME
    string PATH + /

    savepos LIMIT
    math LIMIT + ENTRY_SIZE
    for
        savepos TMP
        if TMP u>= LIMIT
            break
        endif

        get IS_FOLDER byte
        if IS_FOLDER != 0
            get ENTRY_SIZE long
            get NAME string
            callfunction GET_TREE
        else
            get ID long
            get NAME string
            string NAME p "%s%s" PATH NAME
            if ID >= 50000000
                math ID - 50000000
                putarray 5 ID NAME
                math ID = -1
            endif
            putarray 4 ID NAME
        endif
    next
endfunction

startfunction OPEN_FILE
    open FDSE TMP 0 EXISTS
    if EXISTS == 0
        string TMP + ".CP"
        open FDSE TMP
        goto -4
        savepos ZSIZE
        get SIZE long
        # comtype lzo1x # already set at the beginning of the script
        clog TEMPORARY_FILE 0 ZSIZE SIZE
        open "." TEMPORARY_FILE 1
        open 1
    endif
endfunction

startfunction GET_INFO
    get OFFSET longlong
    get FLAGS short # 1
    get ZSIZE long
    get SIZE long
    putarray 0 -1 OFFSET
    putarray 1 -1 FLAGS
    putarray 2 -1 ZSIZE
    putarray 3 -1 SIZE
endfunction
