# Avatar PowerUp Heroes PCK

endian big
idstring "AKPK"
get INFO_SIZE long
endian guess INFO_SIZE
get DUMMY long  # 1
get NAMES_SIZE long
get DUMMY long
get DUMMY long
savepos BASE_OFF
get NUM long
for i = 0 < NUM
    get NAME_OFF long
    get ID long

    savepos TMP
    math NAME_OFF + BASE_OFF
    goto NAME_OFF
    get NAME string
    goto TMP
    putarray 0 ID NAME
next i
math BASE_OFF + NAMES_SIZE
goto BASE_OFF
do
    get FILES long
while FILES == 0
for i = 0 < FILES
    get SOME_CRC long
    get ALIGN long      # 0x800, align or flags?
    get SIZE longlong   # 64bit?
    get OFFSET long
    get ZERO long
    math OFFSET *= 0x800
    log "" OFFSET SIZE
next i
