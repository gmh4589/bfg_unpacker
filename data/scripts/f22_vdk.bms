# demultiplex *.vdk videos from F-22 Lightning II (PC, 1996)
# (c) 2015-06-12 by AlphaTwentyThree of Xentax
# script for QuickBMS http://quickbms.aluigi.org

idstring "xVDK"
get OFFSET long

get SIZE asize
putVarChr MEMORY_FILE SIZE 0
putVarChr MEMORY_FILE2 SIZE 0
putVarChr MEMORY_FILE3 SIZE 0
log MEMORY_FILE 0 0
log MEMORY_FILE2 0 0
log MEMORY_FILE3 0 0

do
   goto OFFSET
   getDstring IDENT 4
   #print "%IDENT%"
   get SIZE long
   savepos OFFSET
   math SIZE -= 8
   append
   if IDENT == "pVDK"
      log MEMORY_FILE OFFSET SIZE
   elif IDENT == "aVDK"
      math OFFSET += 0x10
      math SIZE -= 0x13
      log MEMORY_FILE2 OFFSET SIZE
      math SIZE += 3
   elif IDENT == "iVDK"
      log MEMORY_FILE3 OFFSET SIZE
   endif
   append
   math OFFSET += SIZE
while IDENT != ""

get FNAME basename
get SIZE asize MEMORY_FILE
if SIZE != 0
   string NAME p= "%s.pVDK" FNAME
   log NAME 0 SIZE MEMORY_FILE
endif

get SIZE asize MEMORY_FILE2
if SIZE != 0
   string NAME p= "%s.aVDK" FNAME
   log NAME 0 SIZE MEMORY_FILE2
endif

get SIZE asize MEMORY_FILE3
if SIZE != 0
   string NAME p= "%s.iVDK" FNAME
   log NAME 0 SIZE MEMORY_FILE3
endif