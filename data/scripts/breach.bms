# extract *.xsp/*.spd sound archives from Breach (XBLA)
# (c) 2012-07-05 by AlphaTwentyThree of XeNTaX

open FDDE spd 0
idstring "SPDv1.04" 0
open FDDE xsp 1 EXIST
if EXIST == 0
   open FDDE psp 1 EXIST
   if EXIST == 0
      cleanexit
   endif
endif

endian big
get SIZE_HEADER long 0
get ZERO long 0
get FILES long 0
get OFF_INFO long 0
set OFF_IDS FILES # sorting of info above (game-internal)
math OFF_IDS *= 0x38
math OFF_IDS += OFF_INFO
set OFF_NAMEPOS FILES # pointer to file name
math OFF_NAMEPOS *= 4
math OFF_NAMEPOS += OFF_IDS
set OFFBIAS_NAMES FILES
math OFFBIAS_NAMES *= 4
math OFFBIAS_NAMES += OFF_NAMEPOS

goto 0 1
endian little
get DUMMY long 1 # FILES again
for i = 1 <= FILES
   get OFFSET long 1
   savepos MYOFF 1
   if i < FILES
      get SIZE long 1
      goto MYOFF 1
   else
      get SIZE asize 1
   endif
   math SIZE -= OFFSET
   goto OFFBIAS_NAMES 0
   get NAME string 0
   string NAME += ".wav"
   savepos OFFBIAS_NAMES 0
   log NAME OFFSET SIZE 1
next i