# Ground Control SDF (script 0.2.1a)
#   Ground Countrol 2 requires http://aluigi.org/bms/world_in_conflict.bms
# script for QuickBMS http://quickbms.aluigi.org

idstring "RYS"
get TYPE byte

math TMP = TYPE
math TMP & 0xc0
# KEY3 is useless
if TMP == 0x0
    math KEY3 = 0x40
elif TMP == 0x40
    math KEY3 = 0x8
elif TMP == 0x80
    math KEY3 = 0x80
elif TMP == 0xc0
    math KEY3 = 0x88
else
    math KEY3 = 0
endif

math TYPE & 0x3f
if TYPE == 0x5
    math KEY1 = 0x81E7C3A1
    math KEY2 = 0x7E183C5A  # KEY2 is a constant, but it appears to be the result of "(~4)^KEY1"
elif TYPE == 0x7
    math KEY1 = 0x6D3AE31B
    math KEY2 = KEY1        # no, it doesn't use 0x7E183C5A!
    get ARCHIVE_SIZE long
else
    print "Error: unsupported archive, try http://aluigi.org/bms/world_in_conflict.bms"
    cleanexit
endif

savepos OFFSET
math SIZE = -1
set PATH string ""
set NAME string ""
callfunction EXTRACT

startfunction EXTRACT
    string PATH + NAME
    string PATH + /
    goto OFFSET

    math ENTRIES = SIZE
    if ENTRIES < 0
        callfunction SET_KEY1 1
        get ENTRIES long
        math ENTRIES ^ KEY
    endif

    callfunction SET_KEY1 1
    get SIZE long
    math SIZE ^ KEY
    callfunction INC_KEY 1

    getdstring DATA SIZE

    math KEY ~ SIZE
    math KEY ^ KEY2
    math x = 0
    for SIZE2 = SIZE > 3
        getvarchr TMP DATA x long
        math TMP ^ KEY
        putvarchr DATA x TMP long
        math x + 4
        callfunction INC_KEY 1
        math KEY ^ SIZE2
    next SIZE2 - 4

    for i = 0 < ENTRIES
        math TMP = i
        math TMP * 4
        getvarchr TMP DATA TMP long
        getvarchr SIZE DATA TMP long
        math TMP + 4
        getvarchr OFFSET DATA TMP long
        math TMP + 4
        getvarchr IS_FOLDER DATA TMP byte
        math TMP + 1
        set NAME string ""
        math x = 0
        do
            getvarchr CHR DATA TMP
            math TMP + 1
            putvarchr NAME x CHR
            math x + 1
        while CHR != 0
        if IS_FOLDER == 0
            string NAME p "%s%s" PATH NAME
            if TYPE == 0x7
                savepos TMP
                goto OFFSET
                get XSIZE long
                goto TMP
                math OFFSET + 4
                math SIZE   - 4
                clog NAME OFFSET SIZE XSIZE
            else
                log NAME OFFSET SIZE
            endif
        else
            callfunction EXTRACT
        endif
    next i
endfunction

startfunction SET_KEY1
    math KEY = KEY1
    if TYPE == 0x7
        xmath KEY "(~4) ^ KEY1" # 4 is the size of the field
    endif
endfunction

startfunction INC_KEY
    math TMP = KEY
    math TMP & 0x1f
    math KEY r TMP
endfunction
