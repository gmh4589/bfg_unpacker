# Electronic Arts BIG4/BIGF archives (script 0.1.3)
#   Fifa 12, Harry Potter, NBA Live and so on
# script for QuickBMS http://quickbms.aluigi.org

comtype COMP_DK2
endian big
idstring "BIG"
get VER byte
get DUMMY long
get FILES long
get OFFSETX long
for i = 0 < FILES
    if VER >= '5' && VER <= '9'
        get ZERO byte
    endif
    get OFFSET long
    get ZSIZE long
    get NAME string

    if ZSIZE == 0
        log NAME OFFSET ZSIZE
    else
        savepos TMP
        goto OFFSET
        get TYPE short
        get SIZE threebyte
        goto TMP

        if TYPE == 0x10fb
            clog NAME OFFSET ZSIZE SIZE
        else
            log NAME OFFSET ZSIZE
        endif
    endif
next i
