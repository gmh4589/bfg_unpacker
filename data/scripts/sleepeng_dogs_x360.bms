# extract the SFX.pck from Sleeping Dogs (Xbox 360)
# (c) 0212-08-16 by AlphaTwentyThree of XeNTaX

include "func_getTYPE.bms"
idstring "AKPK"
endian big
goto 0x28
set FOLDER "SFX_1/"
callfunction extract_folder 1
goto 0x3dac
set FOLDER "SFX_2/"
callfunction extract_folder 1


startfunction extract_folder
   get FILES long
   for i = 1 <= FILES
      get NAME_CRC long
      get MULTI long
      get ZERO long
      get SIZE long
      get OFFSET long
      get ZERO long
      math OFFSET *= MULTI
      putVarChr MEMORY_FILE SIZE 0
      log MEMORY_FILE 0 0
      log MEMORY_FILE OFFSET SIZE
      callfunction getTYPE 1
      get NAME basename
      set WNAME FOLDER
      string NAME_CRC p= "%08x" NAME_CRC
      string NAME += "_"
      string NAME += i
      string NAME += "_0x"
      string NAME += NAME_CRC
      string NAME += EXT
      endian big
      string WNAME += NAME
      log WNAME OFFSET SIZE
   next i
endfunction