# Pinball Heroes
# script for QuickBMS http://quickbms.aluigi.org

get EXT extension
if EXT == "ctl"
    open FDDE "skx"
    get EXT extension
endif

if EXT == "skx"

    idstring "SKEX"
    get DUMMY long
    get ZERO long
    get ZERO long
    get OFFSET long
    get DUMMY long
    get FILES long
    goto OFFSET
    idstring "STBL"
    get DUMMY long
    get FILES long
    get SIZE long
    get ZERO long
    get ZERO long
    get ALIGN long
    get SIZE long
    math OFFSET + SIZE
    goto OFFSET
    get DUMMY long
    for i = 0 < FILES
        get DUMMY short
        get OFFSET long
        math OFFSET * ALIGN
        putarray 0 i OFFSET
    next i

        #get OFFSET asize
        #putarray 0 i OFFSET
        math FILES - 1

    for i = 0 < FILES
        getarray OFFSET 0 i
        math i + 1
        getarray SIZE   0 i
        math SIZE - OFFSET
        log "" OFFSET SIZE
    next

elif EXT == "dam"

    idstring "DAM"  # "DAM0001"
    goto 8
    get FILES long
    get DUMMY long
    get BASE_OFF long
    savepos INFO_OFF
    for i = 0 < FILES
        get OFFSET long
        get SIZE long
        get XSIZE long   # uncompressed size? this is invented by me!
        get NAME_OFF long

        savepos TMP
        math NAME_OFF + INFO_OFF
        goto NAME_OFF
        get NAME string
        goto TMP

        math OFFSET + BASE_OFF
        if XSIZE == 0
            log NAME OFFSET SIZE
        else
            clog NAME OFFSET SIZE XSIZE
        endif
    next i

else

    print "Error: unsupported file %EXT%"
    cleanexit

endif
