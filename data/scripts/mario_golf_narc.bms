# Mario Golf: World Tour (NARC)
# script for QuickBMS http://quickbms.aluigi.org

#comtype ??? # probably a new version of lz77wii with tag 0x60, just guessing
idstring "NARC"
get DUMMY short # -2
get ZERO short
get ARCHIVE_SIZE long
get DUMMY short
get DUMMY short # number of chunks?
savepos TYPE_OFF
for TYPE_OFF = TYPE_OFF != ARCHIVE_SIZE
    getdstring TYPE 4
    get TYPE_SIZE long

    if TYPE u== "BTAF"
        get FILES long
        for i = 0 < FILES
            get OFFSET long
            get END_OFFSET long
            putarray 0 i OFFSET
            putarray 1 i END_OFFSET
        next i

    elif TYPE u== "BTNF"
        get DUMMY long
        get DUMMY long
        for i = 0 < FILES   # FILES is set by BTAF
            get NAMESZ byte
            getdstring NAME NAMESZ
            putarray 2 i NAME
        next i

    elif TYPE u== "GMIF"
        savepos BASE_OFF
        for i = 0 < FILES
            getarray OFFSET     0 i
            getarray END_OFFSET 1 i
            getarray NAME       2 i
            xmath SIZE "END_OFFSET - OFFSET"
            math OFFSET + BASE_OFF

            log NAME OFFSET SIZE
            #clog NAME OFFSET SIZE SIZE
        next i

    else
        print "Alert: unknown chunk type %TYPE%"
    endif

    math TYPE_OFF + TYPE_SIZE
    goto TYPE_OFF
next
