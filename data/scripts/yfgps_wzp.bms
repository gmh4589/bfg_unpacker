# YF GPS device WZP (YFAPP.wzp) files extractor
# script for QuickBMS http://quickbms.aluigi.org

get OFFSET asize
math OFFSET -= 0x100
goto OFFSET
findloc OFFSET string "\xff\xd9\x03\x04"
goto OFFSET
get SIGN long
get FILES short
get SIZE long
get OFFSET2 long

math OFFSET = 0
for i = 0 < FILES
    goto OFFSET
    get SIGN short
    if SIGN != 0xd8ff
        print "invalid signature at offset %OFFSET%"
        cleanexit
    endif
    get TYPE short
    get ver             short
    get flag            short
    get method          short
    get modtime         short
    get moddate         short
    get crc             long
    get name_len        short
    getdstring name     name_len
    savepos OFFSET

    goto OFFSET2
    get SIGN short
    if SIGN != 0xd8ff
        print "invalid signature at offset %OFFSET%"
        cleanexit
    endif
    get TYPE short
    get ver_made        short
    get ver_need        short
    get flag            short
    get method          short
    get modtime         short
    get moddate         short
    get crc             long
    get name_len        short
    get CHUNKS          long
    getdstring name     name_len

    math TMP = CHUNKS
    math TMP *= 16384
    putvarchr MEMORY_FILE TMP 0
    log MEMORY_FILE 0 0

    append
    for j = 0 < CHUNKS
        get uncomp_size     long
        get comp_size       long
        get ext_attr        long

        if method == 0
            log MEMORY_FILE OFFSET uncomp_size
        else
            clog MEMORY_FILE OFFSET comp_size uncomp_size
        endif
        math OFFSET += comp_size
    next j
    append
    savepos OFFSET2

    get SIZE asize MEMORY_FILE
    log name 0 SIZE MEMORY_FILE
next i
