# Tough Trucks - Modified Monsters (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

idstring "bzf2"
get DATE long
get INFO_SIZE long
get FILES long
savepos OFFSET

math INFO_SIZE += 3
math INFO_SIZE &= 0xfffffffc
math INFO_SIZE -= 16

# modified XXTEA
set MEMORY_FILE3 binary "\x53\x8b\x5c\x24\x10\x85\xdb\x57\x0f\x84\x9d\x00\x00\x00\xb8\x34\x00\x00\x00\x33\xd2\xf7\xf3\xbf\x56\xda\x4c\xb5\x69\xc0\x47\x86\xc8\x61\x2b\xf8\x0f\x84\x81\x00\x00\x00\x8b\x4c\x24\x0c\x55\x56\xeb\x04\x8b\x5c\x24\x1c\x8b\xf7\x8d\x43\xff\xc1\xee\x02\x83\xe6\x03\x85\xc0\x76\x34\x8b\x54\x81\xfc\x8b\xda\x8b\xea\xc1\xeb\x05\xc1\xe5\x04\x33\xdd\x8b\x6c\x24\x18\x03\xda\x8b\xd0\x83\xe2\x03\x33\xd6\x8b\x54\x95\x00\x8b\x2c\x81\x03\xd7\x33\xda\x2b\xeb\x89\x2c\x81\x48\x75\xd0\x8b\x5c\x24\x1c\x8b\x54\x99\xfc\x83\xe0\x03\x8b\xda\x8b\xea\xc1\xeb\x05\xc1\xe5\x04\x33\xdd\x33\xc6\x03\xda\x8b\x54\x24\x18\x8b\x04\x82\x8b\x11\x03\xc7\x33\xd8\x2b\xd3\x81\xc7\x47\x86\xc8\x61\x89\x11\x75\x89\x5e\x5d\x5f\x5b\xc3"
log MEMORY_FILE OFFSET INFO_SIZE
set MEMORY_FILE2 binary "\x76\x1b\xd4\xfe\xe0\x2b\xd3\x89\x5a\x7a\xda\x10\xcc\x4a\xdd\x67"
math INFO_SIZE /= 4

# check if the info table is encrypted, unfortunately don't exist other ways
get TYPE byte MEMORY_FILE
get OFFSET long MEMORY_FILE
goto 0 MEMORY_FILE
set do_encryption long 0
if TYPE == 4
elif TYPE == 5
else
    set do_encryption long 1
endif
math OFFSET >>= 24
math OFFSET &= 0xff
if OFFSET != 0x00
    set do_encryption long 1
endif
if do_encryption != 0
    calldll MEMORY_FILE3 0 cdecl RET MEMORY_FILE MEMORY_FILE2 INFO_SIZE
endif

#extraction
for i = 0 < FILES
    get TYPE byte MEMORY_FILE
    get OFFSET long MEMORY_FILE
    get SIZE long MEMORY_FILE
    get ZSIZE long MEMORY_FILE
    get DUMMY long MEMORY_FILE
    get NAMESZ short MEMORY_FILE
    getdstring NAME NAMESZ MEMORY_FILE

    if TYPE == 4
        log NAME OFFSET SIZE
    elif TYPE == 5
        clog NAME OFFSET ZSIZE SIZE
    else
        print "Error: unknown TYPE %TYPE%"
        cleanexit
    endif
next i
