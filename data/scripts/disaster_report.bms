# Disaster Report
#   SOS Final Escape
#   Zettai Zetsumei Toshi
# script for QuickBMS http://quickbms.aluigi.org

open FDDE "FAT" 0 EXISTS
if EXISTS != 0
    math FAT_FILE = 1
else
    math FAT_FILE = 0
    open FDDE "DAT"
endif
idstring "FAT "
get FILES long
goto 0xf8
get NAMES_OFF long
get BASE_OFF long
for i = 0 < FILES
    get OFFSET long
    get SIZE long
    get DUMMY long
    get NAME_OFF long
    savepos TMP
    goto NAME_OFF
    get NAME string
    goto TMP
    math OFFSET += BASE_OFF
    putarray 0 i OFFSET
    putarray 1 i SIZE
    putarray 2 i NAME
next i

if FAT_FILE = 1
    open FDDE "DAT"
endif
for i = 0 < FILES
    getarray OFFSET 0 i
    getarray SIZE 1 i
    getarray NAME 2 i
    log NAME OFFSET SIZE
next i
