# FAT2 and FAT3 Ubisoft games Dunia filesystem (sciprt 0.1.6)
#   Far Cry 3
#   Far Cry 4
#   Watch Dogs
#   The Crew
#   The Crew 2
#   Far Cry: Vengeance
# script for QuickBMS http://quickbms.aluigi.org

quickbmsver "0.6"

open "FDDE" "fat" 0
open "FDDE" "dat" 1

set NAME_CRC = 0    # for archives without NAME_CRC
open "FDDE" "nfo" 2 NFO_EXISTS
if NFO_EXISTS != 0
    idstring 2 "DAT2"
    get NAMES long 2
    for i = 0 < NAMES
        get NAME_CRC long 2
        getdstring NAME 0xc0 2
        putarray 10 i NAME_CRC
        putarray 11 i NAME
    next i
endif

getdstring FAT_SIGN 4
if FAT_SIGN == "3TAF"
    set FAT_SIGN string "FAT3"
elif FAT_SIGN == "FAT3"
    endian big
elif FAT_SIGN == "2TAF"
    set FAT_SIGN string "FAT2"
elif FAT_SIGN == "FAT2"
    endian big
elif FAT_SIGN == "DAT2"
elif FAT_SIGN == "2TAD"
    endian big
    set FAT_SIGN string "DAT2"
else
    print "Error: invalid FAT signature %FAT_SIGN%"
    cleanexit
endif

if FAT_SIGN == "DAT2"
    # nothing
else
    get VERSION long
    get DUMMY long

    if FAT_SIGN == "FAT2"
    if VERSION >= 9
        xmath FLAG1 "DUMMY & 0xff"  # platform?
        xmath FLAG2 "(DUMMY >> 8) & 0xff"
        get DUMMY long
        get DUMMY long
    endif
    endif
endif

get FILES long

if FAT_SIGN == "FAT2"
if VERSION >= 9
    if FLAG1 >= 2   # x360 and PS3
        endian big
    endif
endif
endif

for i = 0 < FILES
    if FAT_SIGN == "FAT2"
        if VERSION >= 9
            get NAME_CRC longlong
            get SIZE long
            get OFFSET long
            get ZSIZE long
        else
            get SIZE long
            get ZERO long
            get NAME_CRC longlong
            get ZSIZE long
            get OFFSET long
        endif
        xmath OFFSET "(OFFSET << 2) | (ZSIZE >> 30)"
        xmath ZSIZE "ZSIZE & ((1 << 30) - 1)"
        xmath ZTYPE "SIZE & 3"
        xmath SIZE "SIZE >> 2"
        if VERSION >= 9
            if FLAG2 == 4   # ??? 0x0402 lzma
                math TMP = 1
                if FLAG1 == 3   # PS3
                    math TMP = 0x10
                endif
                math OFFSET += TMP
                math ZSIZE  -= TMP
            endif
        endif
    elif FAT_SIGN == "DAT2"
        get OFFSET long
        get SIZE long
        get NAME_CRC long
    else
        get NAME_CRC long
        get SIZE long
        get ZSIZE long
        get OFFSET long
        xmath OFFSET "(OFFSET << 3) | (ZSIZE >> 29)"
        xmath ZSIZE "ZSIZE & ((1 << 29) - 1)"
        xmath ZTYPE "SIZE & 7"
        xmath SIZE "SIZE >> 3"
    endif

    #print "DEBUG_INFO %OFFSET|x% %ZSIZE|x% %SIZE|x% %ZTYPE%"

    if SIZE == 0
        if ZSIZE > 0
            callfunction GET_NAME_FROM_CRC 1
            log NAME OFFSET ZSIZE 1
        endif

    elif FAT_SIGN == "FAT2"
        if VERSION >= 9
              if ZTYPE == 0
                comtype lzma_dynamic
            elif ZTYPE == 1
                comtype lzo1x
            elif ZTYPE == 2
                comtype deflate #???
            elif ZTYPE == 3
                comtype xmemdecompress #???
            endif
        else
            if VERSION == 6 # The Crew 2
                comtype oodle
            else
                comtype lzo1x
            endif
        endif
        callfunction GET_NAME_FROM_CRC 1
        clog NAME OFFSET ZSIZE SIZE 1
    elif FAT_SIGN == "DAT2"
        callfunction GET_NAME_FROM_CRC 1
        log NAME OFFSET SIZE 1
    else
        goto OFFSET 1
        get TMP long 1
        if TMP == 0xee12f50f    # will be implemented by default in quickbms in the next version
        
            endian big  # no way to save current endianess in quickbms at the moment

            # from xcompress_file.bms
            get ContextFlags long 1
              get Version short 1   # ???
              get Reserved short 1  # ???
              get Flags long 1
              get WindowSize long 1
              get CompressionPartitionSize long 1
            get UncompressedSizeHigh long 1
            get UncompressedSizeLow long 1
            get CompressedSizeHigh long 1
            get CompressedSizeLow long 1
            get UncompressedBlockSize long 1
            get CompressedBlockSizeMax long 1

            string ARGS p= "%d %d" WindowSize CompressionPartitionSize
            comtype XMemDecompress ARGS
            
            putvarchr MEMORY_FILE UncompressedSizeLow 0 # pre-allocate for speed
            log MEMORY_FILE 0 0 # init
            append
            for MYSIZE = 0 < UncompressedSizeLow
                get CompressedBlockSize long 1
                savepos OFFSET 1
                clog MEMORY_FILE OFFSET CompressedBlockSize UncompressedBlockSize 1
                math OFFSET += CompressedBlockSize
                goto OFFSET 1
                math MYSIZE += UncompressedBlockSize
            next
            append

            endian little   # restore
            
        else
            goto OFFSET 1
            comtype deflate

            math CHUNK_OFFSET = 0
            math CHUNK_SIZE = 0x00010000
            xmath ALIGNMENT "OFFSET & 0xf"
            get CHUNKS short 1
            get DUMMY short 1   # 0xfff

            for x = 0 < CHUNKS
                xmath NEXT_OFFSET "OFFSET + 0x10"
                savepos TMP 1
                if TMP >= NEXT_OFFSET
                    math OFFSET = CHUNK_OFFSET
                    goto OFFSET 1
                    xmath NEXT_OFFSET "OFFSET + 0x10"
                    math CHUNK_OFFSET = 0
                endif
                if CHUNK_OFFSET == 0
                    math CHUNK_OFFSET = NEXT_OFFSET
                endif
                get CHUNK_ZSIZE short 1
                putarray 0 x CHUNK_ZSIZE
                putarray 1 x CHUNK_OFFSET
                math CHUNK_OFFSET += CHUNK_ZSIZE
                for
                    xmath TMP "CHUNK_OFFSET & 0xf"
                    if TMP == ALIGNMENT
                        break
                    endif
                next CHUNK_OFFSET
            next x
     
            putvarchr MEMORY_FILE SIZE 0
            log MEMORY_FILE 0 0
            append
            for x = 0 < CHUNKS
                getarray CHUNK_ZSIZE 0 x
                getarray CHUNK_OFFSET 1 x
                if CHUNK_ZSIZE == 0
                    log MEMORY_FILE CHUNK_OFFSET CHUNK_SIZE 1
                else
                    goto CHUNK_OFFSET 1
                    clog MEMORY_FILE CHUNK_OFFSET CHUNK_ZSIZE CHUNK_SIZE 1
                endif
            next x
            append
        endif
        
        # add missing 0x00 bytes
        putvarchr MEMORY_FILE SIZE 0
        callfunction GET_NAME_FROM_CRC 1
        log NAME 0 SIZE MEMORY_FILE
    endif

next i

startfunction GET_NAME_FROM_CRC
    xmath TMP_CRC "NAME_CRC >> 32"
	string NAME p "%08x%08x" TMP_CRC NAME_CRC
    if NFO_EXISTS != 0
        for z = 0 < NAMES
            getarray TMP_CRC 10 z
            if TMP_CRC == NAME_CRC
                getarray NAME 11 z
                math z = NAMES  # break is bugged
            endif
        next z
    endif
endfunction
