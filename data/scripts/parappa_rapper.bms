# PaRappa the Rapper 2
# script for QuickBMS http://quickbms.aluigi.org

comtype lzss "12 4 2 2 0"
get INT_SIZE asize
math FOLDER = 0
for DATA_OFF = 0 < INT_SIZE
    savepos BASE_OFF
    idstring "\x11\x22\x33\x44"
    get FILES long
    get ID long
    get INFO_OFF long
    get DATA_OFF long
    get DATA_SIZE long
    get ZERO long
    get ZERO long
    math INFO_OFF += BASE_OFF
    math DATA_OFF += INFO_OFF
    if FILES != 0
        for i = 0 < FILES
            get OFFSET long
            putarray 0 i OFFSET
        next i
        goto INFO_OFF
        for i = 0 < FILES
            get NAME_OFF long
            get SIZE long
            putarray 1 i NAME_OFF
            putarray 2 i SIZE
        next i
        savepos OFFSET
        for i = 0 < FILES
            getarray NAME_OFF 1 i
            math NAME_OFF += OFFSET
            goto NAME_OFF
            get NAME string
            putarray 3 i NAME
        next i
        goto DATA_OFF
        get SIZE long
        get ZSIZE long
        savepos OFFSET
        clog MEMORY_FILE OFFSET ZSIZE SIZE
        for i = 0 < FILES
            getarray OFFSET 0 i
            getarray SIZE 2 i
            getarray NAME 3 i
            string FNAME p= "%d/%s" FOLDER NAME
            log FNAME OFFSET SIZE MEMORY_FILE
        next i
    endif
    math DATA_OFF += DATA_SIZE
    goto DATA_OFF
next FOLDER
