# The Evil Within (script 0.1.2a)
# script for QuickBMS http://quickbms.aluigi.org

comtype deflate_noerror # strange deflate, needs the _noerror mode
get SIGN1 byte
idstring "\x94\xab\xcd"
endian big

get EXT extension
if EXT == "tangoresource"
    get FILES long
    for i = 0 < FILES
        get NAMESZ long
        reverselong NAMESZ
        getdstring NAME NAMESZ
        get NAMESZ long
        reverselong NAMESZ
        getdstring NAME NAMESZ
        get DUMMY long
    next i
endif

get OFFSET long
goto OFFSET
get FILES long
get DUMMY long
get DUMMY long
get NUM1 long
get NUM2 long
savepos OFFSET
xmath TMP "OFFSET + ((NUM1 + NUM2) * 2)"
goto TMP

get NAMES_SIZE long
get NAMES long
savepos TMP
for i = 0 < NAMES
    get NAME string
    putarray 0 i NAME
next i
math TMP += NAMES_SIZE
goto TMP
for i = 0 < FILES
    get NAME_IDX long
    get OFFSET long
    get ZSIZE long
    get SIZE long
    getarray NAME 0 NAME_IDX
    if SIZE == ZSIZE
        log NAME OFFSET SIZE
    else
        clog NAME OFFSET ZSIZE SIZE
    endif
next i
