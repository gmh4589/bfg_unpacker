# extract *.xen archives
# supported games:
# - Are You Smarter Than A 5th Grader: Make the Mark (XBLA)
#
# (c) 2012-06-29 by AlphaTwentyThree of XeNTaX

endian big
get UNK long
get DATASTART long
get UNK long
get FILES long

get POS_INFO long
math POS_INFO *= 0x1000
get POS_UNK long
get UNK long
get COMP_SIZE long

goto 0x28
get POS_NAMES long
math POS_NAMES *= 0x1000

get FNAME basename
string FNAME += "/"
goto 0x3c
get DEC_SIZE long
if DEC_SIZE != 0 # compressed data
   get OFFSET asize
   math OFFSET -= COMP_SIZE
   putVarChr MEMORY_FILE 0x15f 0 byte # produce bias of 0x160 bytes
   append
   clog MEMORY_FILE OFFSET COMP_SIZE DEC_SIZE
   append
else
   get SIZE asize
   append
   log MEMORY_FILE 0 SIZE
   append
endif
goto POS_INFO MEMORY_FILE
for i = 1 <= FILES
   get OFFSET long MEMORY_FILE
   math OFFSET *= 0x1000
   get CRC long MEMORY_FILE
   get SIZE long MEMORY_FILE
   get NAMEPOS long MEMORY_FILE
   math NAMEPOS += POS_NAMES
   get UNK long MEMORY_FILE # 0 or 1
   get UNK long MEMORY_FILE # multiple of 4 or 0
   get ZSIZE long MEMORY_FILE
   get ZERO long MEMORY_FILE
   savepos MYOFF MEMORY_FILE
   goto NAMEPOS MEMORY_FILE
   get NAME string MEMORY_FILE
   goto MYOFF MEMORY_FILE
   if SIZE != 0
      set WNAME FNAME
      string WNAME += NAME
      log WNAME OFFSET SIZE MEMORY_FILE
   endif
next i