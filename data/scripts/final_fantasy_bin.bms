# Final Fantasy 2 BIN
# script for QuickBMS http://quickbms.aluigi.org

comtype lzss "12 4 2 2 0"   # lzss 0x00
math OFFSET = 0
set PATH string ""
set NAME string ""
callfunction EXTRACT

startfunction EXTRACT
    string PATH += NAME
    string PATH += /
    goto OFFSET
    savepos BASE_OFF

    idstring "ARCV"
    get FILES long
    getdstring DUMMY 0x38
    for i = 0 < FILES
        get OFFSET long
        get SIZE long
        getdstring NAME 0x38
        math OFFSET += BASE_OFF
        string FNAME p= "%s/%s." PATH NAME  # auto-guess extension

        savepos TMP
        goto OFFSET
        getdstring SIGN 4
        if SIGN == "ARCV"
            callfunction EXTRACT
        elif SIGN == "SSZL"
            get ZERO long
            get ZSIZE long
            get SIZE long
            savepos OFFSET
            clog FNAME OFFSET ZSIZE SIZE
        else
            log FNAME OFFSET SIZE
        endif
        goto TMP
    next i
endfunction
