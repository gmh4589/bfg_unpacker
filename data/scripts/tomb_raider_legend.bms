# Tomb Raider Legend (script 0.2.1)
# script for QuickBMS http://quickbms.aluigi.org

get EXT extension
if EXT == "000"
    # necessary because sortarray is signed
    math TMP = 0x10000000
    math TMP *= 16
    if TMP == 0
        print "you must use quickbms_4gb_files.exe with big archives"
        cleanexit
    endif
endif
get FILES long
endian guess FILES
for i = 0 < FILES
    get NAME_CRC long
next i
for i = 0 < FILES
    get SIZE long
    get OFFSET long
    get DUMMY long
    get ZSIZE long
    math OFFSET * 0x800
    if EXT == "000"
        putarray 0 i OFFSET
        putarray 1 i SIZE
        putarray 2 i ZSIZE
    else
        log "" OFFSET SIZE
    endif
next i
if EXT == "000"
    print "sorting... wait less than one minute..."
    sortarray 0 1
    math ARCHIVE_NUM = 0
    math ARCHIVE_OFF = 0
    get ARCHIVE_SIZE asize
    for i = 0 < FILES
        getarray OFFSET 0 i
        getarray SIZE   1 i
        getarray ZSIZE  2 i
        math TMP_OFF = OFFSET
        math OFFSET - ARCHIVE_OFF
        if OFFSET u>= ARCHIVE_SIZE
            math ARCHIVE_NUM + 1
            string EXT p "%03d" ARCHIVE_NUM
            open FDDE EXT
            math ARCHIVE_OFF + OFFSET
            get ARCHIVE_SIZE asize
            math OFFSET = 0
        endif
        if ZSIZE == 0
            log "" OFFSET SIZE
        else
            clog "" OFFSET ZSIZE SIZE
        endif
    next i
endif
