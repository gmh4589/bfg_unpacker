# Rally Trophy
# script for QuickBMS http://quickbms.aluigi.org

idstring "bbzf"
get DATE long
get FILES long
savepos OFFSET

math SIZE = FILES
math SIZE *= 0x35

set MEMORY_FILE2 binary "\x8b\xc2\x69\x80\x46\x42\x18\x25\x80\x5d\xaa\x02\x6d\x49\x11\x94\xa8\x01\xe7\x58\xcb\x8d\xa6\x30\x3e\x83\x1b\x09\x4c\xce\x18\xbb\x7c\xae\x6a\x30\x1e\x00\x0a\xc9\x24\xc0\x98\xfd\x33\xcc\x04\x44\xfa\xea\x8f\x33\xc3\x0d\xf2\xe8\x47\xd4\xdc\xa8\xc7\x54\xa2\xc7\x58\x17\x32\xc8\x7f\x64\x4a\xc6\xfd\xc1\xe7\x13\xec\xb7\xff\x1e\x0b\xd5\x72\x9a\x57\xf8\x3f\xdc\xdb\xc9\xf3\x85\xc8\x86\x68\x62\xc8\x06\xab\x90\x90\xf7\x3e\xe4\xb4\x6b\x80\x89\xc0\x93\x69\xeb\x84\xc9\x7a\xd4\xaf\xd4\xf4\xd5\xa0\x68\x49\xe8\x78\xed\xd0\x53\x74\x81\xbc\xd0\x7a\x40\x4f\xea\xf1\xc3\x4b\xaa\xd6\xe6\xaa\x73\x0c\xcf\x8e\x2b\xf4\x2c\x7b\x9b\x3e\xbb\xc4\x19\xff\x10\x43\x64\x03\x93\x4d\xcf\x64\xc8\xe5\xa2\x5b\xd3\x30\xbd\x57\x3b\x2a\x7e\x4c\xee\x96\xe5\x4e\x85\x3b\xf6\x5e\xca\x4d\x5f\x84\x77\x2a\x5b\x1e\x41\x47\xd6\x77\x15\x69\xd2\x9b\xa2\x18\x09\x6a\x17\x50\xd4\xec\x2e\x7b\x4b\xe4\x69\x9c\xae\xa8\x9b\xcd\x04\x2f\xab\xeb\x01\x6c\x95\x90\x2d\xda\xb1\x41\x43\x59\x38\xe9\xd7\x38\x04\x86\x3c\x93\x97\x23\xa5\xdf\x5f\x06\x8a\xc3\x39\x2a\x4e\x29\x33\xf0\x1e"

encryption xor MEMORY_FILE2
log MEMORY_FILE OFFSET SIZE
encryption "" ""

for i = 0 < FILES
    get TYPE byte MEMORY_FILE
    get OFFSET long MEMORY_FILE
    get SIZE long MEMORY_FILE
    get ZSIZE long MEMORY_FILE
    getdstring NAME 0x28 MEMORY_FILE

    # in the patch has been introduced an header of 43 bytes
    math SIZE += 100    # in case of other updates

    encryption xor MEMORY_FILE2
    if TYPE == 0
        log NAME OFFSET SIZE
    elif TYPE == 1
        clog NAME OFFSET ZSIZE SIZE
    else
        print "Error: unknown TYPE %TYPE%"
        cleanexit
    endif
    encryption "" ""
next i
