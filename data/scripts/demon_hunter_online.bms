# Demon Hunter Online
# script for QuickBMS http://quickbms.aluigi.org

get INFO_OFF long
goto INFO_OFF
get FILES long
for i = 0 < FILES
    get NAMESZ long
    getdstring NAME NAMESZ
    get DUMMY longlong
    get OFFSET longlong
    get ZIP longlong
    get SIZE longlong

    if ZIP == 0
        log NAME OFFSET SIZE
    else
        savepos BCK_OFF
        goto OFFSET
        get TYPE long
        if TYPE == 0x9e2a83c1
            get CHUNK_SIZE long
            get ZSIZE long
            get SIZE long
            math CHUNKS = SIZE
            math CHUNKS / CHUNK_SIZE
            if SIZE % CHUNK_SIZE
                math CHUNKS + 1
            endif
            savepos CHUNK_OFF
            xmath CHUNK_OFF "CHUNK_OFF + (CHUNKS * (4 + 4))"
            putvarchr MEMORY_FILE SIZE 0
            log MEMORY_FILE 0 0
            append
            for x = 0 < CHUNKS
                get CHUNK_ZSIZE long
                get CHUNK_SIZE long
                clog MEMORY_FILE CHUNK_OFF CHUNK_ZSIZE CHUNK_SIZE
                math CHUNK_OFF + CHUNK_ZSIZE
            next x
            append
            log NAME 0 SIZE MEMORY_FILE
        else
            log NAME OFFSET SIZE
        endif
        goto BCK_OFF
    endif
next i
