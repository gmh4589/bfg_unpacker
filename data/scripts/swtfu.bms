# Star Wars The Force Unleashed / .lp container
# script for QuickBMS http://aluigi.org/papers.htm#quickbms

# The container format seems pretty strange and I don't understand it much, but this script works...

# File Header
IDString "kaPA"
Get @version long
If @version != 5
   Print "@version = %@version%, expected 5."
   CleanExit
EndIf
Get @nFiles long

GoTo 0x1C
Get @fileNameTableSize long
Get @fileOffTableOff long
Get @dataOff long

# Calculate some useful stuff
Set @fileNameTableOff 0x50

Set @fileInfoTableOff @fileNameTableOff
Math @fileInfoTableOff += @fileNameTableSize

# Extract the files
For @i = 0 < @nFiles
   # Get the file name (there's an offset to it at 0x14 of the file info table)
   Set @fileNameOffOff @i
   Math @fileNameOffOff *= 0x40
   Math @fileNameOffOff += @fileInfoTableOff
   Math @fileNameOffOff += 0x14
   GoTo @fileNameOffOff

   Get @fileNameOff long
   Math @fileNameOff += @fileNameTableOff
   GoTo @fileNameOff

   Get @fileName string

   # Get the file size (it's at 0x28 of the file info table)
   Set @fileSizeOff @i
   Math @fileSizeOff *= 0x40
   Math @fileSizeOff += @fileInfoTableOff
   Math @fileSizeOff += 0x28
   Goto @fileSizeOff

   Get @fileSize long

   # Get the file offset (it's at 0x04 of the file offset table)
   Set @fileOffOff @i
   Math @fileOffOff *= 0x10
   Math @fileOffOff += @fileOffTableOff
   Math @fileOffOff += 0x04
   Goto @fileOffOff

   Get @fileOff long
   Math @fileOff += @dataOff
   
   # Extract the data, finally
   Log @fileName @fileOff @fileSize
Next @i