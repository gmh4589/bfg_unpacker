# extract *.bin files from Crystal Defenders (XBLA, WiiWare, PSN, PSP)
#
# (c) 2012-07-08 by AlphaTwentThree of XeNTaX

get IDENT long
get FILES long
goto 0x20
if IDENT == 0x58424201 # XBLA
   endian big
   callfunction XBB 1
elif IDENT == 0x01424258 # PSP/PSN
   callfunction XBB 1
else
   get FSIZE asize
   math FSIZE -= 0x20
   reverseLong IDENT
   if IDENT == FSIZE # WiiWare
      endian big
      reverseLong FILES
      callfunction Wii_bin
   else
      cleanexit
   endif
endif

startfunction XBB
   for i = 1 <= FILES
      get OFFSET long
      get SIZE long
      get NAMEPOS long
      get NAME_CRC long
      if NAMEPOS != 0
         savepos MYOFF
         goto NAMEPOS
         get NAME string
         goto MYOFF
      else
         string NAME_CRC p= "_%08x" NAME_CRC
         get NAME basename
         string NAME += NAME_CRC
      endif
      log NAME OFFSET SIZE
   next i
endfunction
startfunction Wii_bin
   set OFFSET 0x20
   for i = 1 <= FILES
      goto OFFSET
      get SIZE long
      get NAME basename
      string NAME += "_"
      string NAME += i
      log NAME OFFSET SIZE
      math OFFSET += SIZE
   next i
endfunction