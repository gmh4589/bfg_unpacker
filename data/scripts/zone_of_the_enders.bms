# Zone of the Enders
# script for QuickBMS http://quickbms.aluigi.org

set PATH string ""
set NAME string ""
math SUB_ENTRIES = -1
callfunction EXTRACT 1

startfunction EXTRACT
    string PATH + NAME
    string PATH + /
    savepos BACKUP_OFFSET
    goto OFFSET
    math ENTRIES = SUB_ENTRIES
    for i = 0 != ENTRIES
        getdstring NAME 16
        get SUB_ENTRIES long
        get OFFSET long
        get FLAGS long
        get ZERO long
        if ENTRIES < 0 && FLAGS == 0
            break
        endif

        if FLAGS & 0x102    # 0x100 or 2?
            math OFFSET * 0x800
            callfunction EXTRACT
        else
            math SIZE = OFFSET
            math OFFSET = SUB_ENTRIES
            math OFFSET * 0x800
            string NAME p "%s%s" PATH NAME
            log NAME OFFSET SIZE
        endif
    next i
    goto BACKUP_OFFSET
endfunction
