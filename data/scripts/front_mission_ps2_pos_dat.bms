# Front Mission 4/Front Mission Online/Front Mission 5(PS2) - dvdimage.pos/dvdimage.dat

open FDDE "pos" 0
open FDDE "dat" 1

get POS_SIZE asize 0

get POS1 long
xmath POS1_OFFSET "POS1 * 4 + 8"
for CF = 0 < POS1
	get CF1 short
	get CF2 short
next CF
goto POS1_OFFSET
do
	get OFFSET long
	get SIZE long
	math OFFSET * 0x800
	if SIZE != 0
		callfunction BUILD_NAME 1
		log NAME OFFSET SIZE 1
	endif
	savepos CFC_OFF
while CFC_OFF < POS_SIZE

startfunction BUILD_NAME
	goto OFFSET 1
	get SIGN1 short 1
	get SIGN2 short 1
	get SIGN3 short 1
	get SIGN4 short 1
	xmath CFC_COUNT "CFC_OFF / 8"
	if SIGN1 == 0x4954
		if SIGN2 == 0x324d
			string CFC_EXT p "tm2"
		endif
	elif SIGN1 == 0x5356
		if SIGN2 == 0x0046
			string CFC_EXT p "vsf"
		elif SIGN2 == 0x0000
			string CFC_EXT p "vs"
		endif
	elif SIGN1 == 0x4742
		string CFC_EXT p "bgm"
	elif SIGN1 == 0x4457
		string CFC_EXT p "wd"
	elif SIGN1 == 0x534d
		if SIGN2 == 0x0047
			string CFC_EXT p "msg"
		endif
	elif SIGN1 == 0x4353
		string CFC_EXT p "scp"
	elif SIGN1 == 0x7468
		string CFC_EXT p "dat"
	elif SIGN1 == 0
		if SIGN2 == 0xba01
			string CFC_EXT p "pss"
		endif
	else
		set CFC_EXT string ""
	endif
	string NAME p= "%08d.%s" CFC_COUNT CFC_EXT
endfunction
