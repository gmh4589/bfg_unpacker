# GetAmped 2 (script 0.2)
# script for QuickBMS http://quickbms.aluigi.org

endian big
get KEY long
get OFFSET long
math OFFSET ^ KEY
goto OFFSET
get SIZE long
savepos OFFSET
xmath XOR_KEY "KEY & 0xff"
filexor XOR_KEY
log MEMORY_FILE OFFSET SIZE
filexor ""

set PATH string ""
set NAME string ""
callfunction EXTRACT

startfunction EXTRACT
    get NAMESZ short MEMORY_FILE
    getdstring NAME NAMESZ MEMORY_FILE
    get DUMMY long MEMORY_FILE
    get DUMMY long MEMORY_FILE
    get DUMMY long MEMORY_FILE
    get DUMMY long MEMORY_FILE
    get TYPE byte MEMORY_FILE
    if TYPE != 0
        string PATH += NAME
        string PATH += /
        get SUB_FOLDERS short MEMORY_FILE
        for i = 0 < SUB_FOLDERS
            callfunction EXTRACT
        next i
    else
        get ZERO byte MEMORY_FILE   # maybe it's the compression?
        get OFFSET long MEMORY_FILE
        get SIZE long MEMORY_FILE
        set FNAME string PATH
        string FNAME += NAME

        math XOR_KEY = KEY
        math XOR_KEY + OFFSET
        math XOR_KEY & 0xff
        filexor XOR_KEY
        log FNAME OFFSET SIZE
        filexor ""
    endif
endfunction
