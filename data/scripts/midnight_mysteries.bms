# Midnight Mysteries (script 0.2.2)
#   Salem Witch Trials, Samantha Swift, Fountains of Fate, Ghostwriter
# script for QuickBMS http://quickbms.aluigi.org

comtype deflate_noerror # there is no decompressed size
get SIGN long
if SIGN == 0xeceddece

    get VERSION long    # 1
    get OFFSET long
    goto OFFSET
    get FILES long

    savepos TMP
        get TYPE long
        get OFFSET long
    goto TMP
    math IS64 = 0
    if OFFSET == 0
        math IS64 = 1
    endif

    for i = 0 < FILES
        if IS64 == 0
            get TYPE long
            get OFFSET long
            get SIZE long
            get NAME_OFF long
        else
            get OFFSET longlong
            get SIZE longlong
            get TYPE long
            get NAME_OFF long
        endif
        putarray 0 i TYPE
        putarray 1 i OFFSET
        putarray 2 i SIZE
        putarray 3 i NAME_OFF
    next i

    get SIZE long
    savepos NAMES_OFF

    set PATH string ""
    for i = 0 < FILES
        getarray TYPE     0 i
        getarray OFFSET   1 i
        getarray SIZE     2 i
        getarray NAME_OFF 3 i

        math NAME_OFF + NAMES_OFF
        goto NAME_OFF
        get NAME string

        if TYPE == 0
            set FNAME string PATH
            string FNAME + NAME
            log FNAME OFFSET SIZE
        else
            set PATH string NAME
        endif
    next i

else

    get DUMMY long
    get VERSION long  # 6
    get INFO_OFF long
    goto INFO_OFF
    get FILES long
    for i = 0 < FILES
        get NAMESZ byte
        getdstring NAME NAMESZ
        get DUMMY long
        get DUMMY long
        get OFFSET long
        get ZSIZE long
        get SIZE long
        get ZIP long
        if ZIP == 0
            if ZSIZE == SIZE
                log NAME OFFSET SIZE
            else
                clog NAME OFFSET ZSIZE SIZE # ???
            endif
        else
            clog NAME OFFSET SIZE SIZE
        endif
    next i

endif
