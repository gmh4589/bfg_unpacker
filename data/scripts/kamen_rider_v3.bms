# KAZe/digifloyd - SLPS_###.##(related to [V3/KU/AGT]FILE.BIN/RUDATA.BIN)
# the executables have all the data related into the entire CD, for now it's only limited into these archives

open FDSE "SLPS_029.64" 0 EXISTS	# Kamen Rider V3(KAZe)
if EXISTS != 0
	# we get the archive name from the executable, which is enough info to make sure
	# that same archive is loaded as a second file in this script
	goto 0x62a98
	callfunction ADJUST_DIR 1
	goto 0x62aa8
	for i = 0 < 0x176
		callfunction GET_TOC_FROM_EXE 1
	next i
	else
		open FDSE "SLPS_030.90" 0 EXISTS	# Kamen Rider Kuuga(KAZe)
		if EXISTS != 0
			goto 0x6f1d0
			callfunction ADJUST_DIR 1
			goto 0x6f1e0
			for i = 0 < 0xef
				callfunction GET_TOC_FROM_EXE 1
			next i
		else
			open FDSE "SLPS_033.44" 0 EXISTS	# Kamen Rider Agito(KAZe)
			if EXISTS != 0
				goto 0x52164
				callfunction ADJUST_DIR 1
				goto 0x52174
				for i = 0 < 0x107
					callfunction GET_TOC_FROM_EXE 1
				next i
			else
				open FDSE "SLPS_034.95" 0 EXISTS	# Kamen Rider Ryuki(digifloyd)
				if EXISTS != 0
					# it's not just "RUDATA.BIN" however, it can also involve "RY_STR00.STR" and "RY_XA00.XA" as well
					goto 0x3338
					callfunction ADJUST_DIR 1
					goto 0x3368
					# this is literally the total number of files on RUDATA.BIN
					for i = 0 < 0x326
						get OFFSET short
						get SIZE short
						math OFFSET * 0x800
						math SIZE * 0x800
						string DIR - ".BIN"
						string FNAME p= "%s_%04d." DIR i
						log FNAME OFFSET SIZE 1
					next i
				else
					print "Unsupported executable. Contact me."
					cleanexit
				endif
			endif
		endif
	endif
endif

startfunction ADJUST_DIR
	get DIR string
	string DIR - \
	string DIR - ";1"
	open FDSE DIR 1
endfunction

startfunction GET_TOC_FROM_EXE
		get OFFSET long
		get SIZE long
		math OFFSET * 0x800
		math SIZE * 0x800
		string DIR - ".BIN"
		string FNAME p= "%s_%04d." DIR i
		log FNAME OFFSET SIZE 1
endfunction
