# Dungeon Siege DSigTank (script 0.2a)
# script for QuickBMS http://quickbms.aluigi.org

#idstring "DSigTank"
idstring "DS"
getdstring DUMMY 2  # DSigTank and DSg2Tank
idstring "Tank"
get DUMMY short
get DUMMY short
get FOLDERS_OFF long
get FILES_OFF long
get DUMMY long
get BASE_OFF long

xmath TMP "FILES_OFF - FOLDERS_OFF"
log MEMORY_FILE2 FOLDERS_OFF TMP

# useless
goto FOLDERS_OFF
get FILES long
for i = 0 < FILES
    get OFFSET long
    math OFFSET += FOLDERS_OFF
    putarray 0 i OFFSET
next i
for i = 0 < FILES
    getarray OFFSET 0 i
    goto OFFSET
    get DUMMY long
    get DUMMY long
    get TSTAMP longlong
    get NAMESZ short
    math NAMESZ += 1
    getdstring NAME NAMESZ
    padding 4
    get DUMMY long
    get DUMMY long
next i

goto FILES_OFF
get FILES long
for i = 0 < FILES
    get OFFSET long
    math OFFSET += FILES_OFF
    putarray 0 i OFFSET
next i
for i = 0 < FILES
    getarray OFFSET 0 i
    goto OFFSET
    get FOLDER_OFF long
    get SIZE long
    if SIZE != 0
        get OFFSET long
        math OFFSET += BASE_OFF
        get DUMMY long          # CRC/NAME_CRC?
        get TSTAMP longlong
        get CHUNKED long
        get NAMESZ short
        math NAMESZ += 1
        getdstring NAME NAMESZ
        padding 4

        set PATH string ""
        callfunction GET_FOLDER
        getarray PATH 1 0
        string FNAME p= "%s/%s" PATH NAME

        putvarchr MEMORY_FILE SIZE 0
        log MEMORY_FILE 0 0
        append
        if CHUNKED == 0
            log MEMORY_FILE OFFSET SIZE
        else
            get DUMMY long
            get CHUNK_SIZE long
            do
                get CHUNK_SIZE long
                get CHUNK_ZSIZE long
                get CHUNK_MORE long
                get CHUNK_OFFSET long
                math CHUNK_OFFSET += OFFSET
                if CHUNK_ZSIZE == CHUNK_SIZE
                    log MEMORY_FILE CHUNK_OFFSET CHUNK_SIZE
                else
                    clog MEMORY_FILE CHUNK_OFFSET CHUNK_ZSIZE CHUNK_SIZE
                endif
                math CHUNK_OFFSET += CHUNK_ZSIZE
                log MEMORY_FILE CHUNK_OFFSET CHUNK_MORE
            while CHUNK_MORE != 0
        endif
        append

        log FNAME 0 SIZE MEMORY_FILE
    endif
next i

startfunction GET_FOLDER
    if FOLDER_OFF == 0
        putarray 1 0 PATH   # store it in an array because it's a recursive function
    else
        goto FOLDER_OFF MEMORY_FILE2
        get PREV_OFFSET long MEMORY_FILE2
        get DUMMY long MEMORY_FILE2
        get TSTAMP longlong MEMORY_FILE2
        get NAMESZ short MEMORY_FILE2
        math NAMESZ += 1
        getdstring NAME NAMESZ MEMORY_FILE2
        padding 4 MEMORY_FILE2
        get DUMMY long MEMORY_FILE2
        get DUMMY long MEMORY_FILE2

        string PATH p= "%s/%s" NAME PATH

        math FOLDER_OFF = PREV_OFFSET
        callfunction GET_FOLDER
    endif
endfunction
