# Double Dragon Neon (script 0.1.1)
# script for QuickBMS http://quickbms.aluigi.org

get EXT extension
if EXT == "ltb"

    # I don't think this data must be extracted... this is just a sort of documentation
    get FILES long
    for i = 0 < FILES
        idstring "ZLFW"
        get SIZE long
        get DUMMY long
        get SUBS long           # ???
        get HEADER_SIZE long    # ???
        get DUMMY long
        get DUMMY long
        get DUMMY long
        savepos OFFSET
        math SIZE - 0x10
        log "" OFFSET SIZE
        math OFFSET + SIZE
        goto OFFSET
    next i

else    # "pak"

    get BASE_OFF long

    savepos TMP
    goto BASE_OFF
    getdstring TEST 0x40
    if TEST & "MANAGEDFILE_DATABLOCK"
        savepos BASE_OFF
    endif
    goto TMP

    math SKIP = 0
    get FILES long
    for i = 0 < FILES
        idstring "FILELINK"
        if i == 0
            getdstring TEST 8
            goto -8 0 SEEK_CUR
            if TEST == "________"
                math SKIP = 8
            endif
        endif
        getdstring DUMMY SKIP
        get OFFSET long
        get SIZE long
        get NAME string
        padding 4
        math OFFSET + BASE_OFF
        log NAME OFFSET SIZE
    next i

endif
