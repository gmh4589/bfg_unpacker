# extracts the raw audio stream from Sega Saturn and Sega CD movies with FILM signature
# e.g. found in:
# - 3 Ninjas Kick Back (SCD)
# - Shellshock (SS)
# - Tomb Raider (SS)
# - X-Men: Children of the Atom (SS)
# (c) 2012-08-09 by AlphaTwentyThree of XeNTaX

endian big
idstring "FILM"
get BIAS long
getDstring VER 4
get ZERO long
idstring "FDSC"
get STAB_OFF long
math STAB_OFF += 0x10
goto STAB_OFF

idstring "STAB"
get STAB_SIZE long
get UNK long
get ENTRIES long
savepos MYOFF
set OFF_HEADER ENTRIES
math OFF_HEADER *= 0x10
math OFF_HEADER += MYOFF
set SIZE_HEADER BIAS
math SIZE_HEADER -= OFF_HEADER

set PSIZE 0
for p = 0 < 2
   for i = 1 <= ENTRIES
      get OFFSET long
      math OFFSET += BIAS
      get SIZE long
      get UNK long
      get TYPE long
      if TYPE == 1 # audio
         if p == 0
            math PSIZE += SIZE
         else
            append
            log MEMORY_FILE OFFSET SIZE
            append
         endif
      endif
   next i
   if p == 0
      putVarChr MEMORY_FILE PSIZE 0
      log MEMORY_FILE 0 0
   endif
   goto MYOFF
next p
get SIZE asize MEMORY_FILE
get NAME basename
string NAME += ".raw"
log NAME 0 SIZE MEMORY_FILE
get NAME basename
string NAME += ".info"
log NAME OFF_HEADER SIZE_HEADER