# extracts EA big archives with "EB" identifier
# tested with NHL 13 (X360)
# (c) 2012-10-08 by AlphaTwentyThree of XeNTaX

comtype xmemdecompress
idstring \x45\x42\x00\x03
endian big
goto 4
get FILES long
get UNK long # some size
get OFF_NAMES long # plus random bias of zeros
get SIZE_NAMES long
get NAMEL_FILE byte
get NAMEL_FOLDER byte
get ZERO byte
get FOLDERS byte
set OFF_FOLDERS FILES
math OFF_FOLDERS *= NAMEL_FILE
math OFF_FOLDERS += OFF_NAMES
math OFF_FOLDERS x= 0x10 # round to next 0x10 bytes
math NAMEL_FILE -= 2 # short for folder number

goto 0x30
for i = 1 <= FILES
   get OFFSET long
   math OFFSET *= 0x10
   get ZERO long
   get SIZE long
   get HASH long
   putArray 0 i OFFSET
   putArray 1 i SIZE
next i
goto OFF_NAMES
for i = 1 <= FILES
   get FOLDER_NUMBER short
   getDstring NAME NAMEL_FILE
   savepos MYOFF
   set OFF_FOLDERNAME FOLDER_NUMBER
   math OFF_FOLDERNAME *= NAMEL_FOLDER
   math OFF_FOLDERNAME += OFF_FOLDERS
   goto OFF_FOLDERNAME
   getDstring FOLDER NAMEL_FOLDER
   set FNAME FOLDER
   string FNAME += "/"
   string FNAME += NAME
   getArray OFFSET 0 i
   getArray SIZE 1 i
   goto OFFSET
   getDstring COMP 8
   if COMP == "chunklzx"
      callfunction unpack 1
      log FNAME 0 SIZE MEMORY_FILE
   else
      log FNAME OFFSET SIZE
   endif
   goto MYOFF
next i

startfunction unpack # (c) Luigi Auriemma
    get DUMMY long
    get FULLSIZE long
    get SIZE long
    get CHUNKS long
    get DUMMY long
    get DUMMY long
    get DUMMY long
    get DUMMY long
    log MEMORY_FILE 0 0
    append
    savepos OFFSET
    for j = 0 < CHUNKS
        math OFFSET x= 0x8
        for # very lame, made on the fly
            math T = OFFSET
            math T %= 0x10
            if T == 8
                break
            endif
            math OFFSET += 8
        next
        goto OFFSET
        get ZSIZE long
        get DUMMY long
        savepos OFFSET
        clog MEMORY_FILE OFFSET ZSIZE SIZE
        math OFFSET += ZSIZE
    next j
    append
    get MYSIZE asize MEMORY_FILE
    if MYSIZE != FULLSIZE
        print "Alert: MEMORY_FILE %MYSIZE% != %FULLSIZE%"
    endif
   set SIZE FULLSIZE
endfunction