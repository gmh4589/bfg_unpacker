# Final Fantasy X/X-2 HD Remaster (script 0.2.1)
# script for QuickBMS http://quickbms.aluigi.org

comtype zlib_noerror
idstring "SRYK"
get BASE_OFF long
get FILES long
get ZERO long
for i = 0 < FILES
    getdstring HASH 0x10
next i
for i = 0 < FILES
    get IDX long
    get DUMMY long
    get SIZE longlong
    get OFFSET longlong
    get NAME_OFF longlong
    putarray 0 i IDX
    putarray 1 i OFFSET
    putarray 2 i SIZE
    putarray 3 i NAME_OFF
next i

get SIZE long
math SIZE - 4
savepos TMP
log MEMORY_FILE2 TMP SIZE
math CHUNKS_OFF = TMP
math CHUNKS_OFF + SIZE

for i = 0 < FILES
    getarray IDX      0 i
    getarray OFFSET   1 i
    getarray SIZE     2 i
    getarray NAME_OFF 3 i

    goto NAME_OFF MEMORY_FILE2
    get NAME string MEMORY_FILE2

    xmath TMP "CHUNKS_OFF + (IDX * 2)"
    goto TMP

    putvarchr MEMORY_FILE SIZE 0
    log MEMORY_FILE 0 0
    append
    for MEM_SIZE = 0 u< SIZE
        get CHUNK_SIZE short
        clog MEMORY_FILE OFFSET CHUNK_SIZE 0x10000
        get MEM_SIZE asize MEMORY_FILE
        math OFFSET + CHUNK_SIZE
    next
    append

    log NAME 0 SIZE MEMORY_FILE
next i
