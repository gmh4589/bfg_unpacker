# Iron Sky: Invasion (2012) PC - *.gpk extractor ( input file: *.mod)
# (c) 2012-11-30 by AlphaTwenyThree of XeNTaX

idstring "<?xml"
get NAME filename
string NAME -= 8
string NAME += ".gpk"
open FDSE NAME 1

for i = 0
   FindLoc POS string "<File alias=" 0 ""
   if POS == ""
      break
   endif
   math POS += 0x12
   goto POS 0
   getCT NAME string 0x22 0
   FindLoc POS string "size=" 0 ""
   math POS += 10
   goto POS 0
   get TEST byte 0
   if TEST != 0x20
      goto MYOFF 0
   endif
   getCT NUM string 0x22 0
   callfunction str2num 1
   set ZSIZE NUM
   FindLoc POS string "originalSize=" 0 ""
   math POS += 18
   goto POS 0
   get TEST byte 0
   if TEST != 0x20
      goto MYOFF 0
   endif
   getCT NUM string 0x22 0
   callfunction str2num 1
   set SIZE NUM
   FindLoc POS string "offset=" 0 ""
   math POS += 12
   goto POS 0
   get TEST byte 0
   if TEST != 0x20
      goto MYOFF
   endif
   getCT NUM string 0x22 0
   callfunction str2num 1
   set OFFSET NUM
   if SIZE != ZSIZE
      savepos MYOFF 0
      goto OFFSET 1
      callfunction decompress 1
      get SIZE asize MEMORY_FILE
      log NAME 0 SIZE MEMORY_FILE
      goto MYOFF 0
   else
      log NAME OFFSET ZSIZE 1
   endif
next i

startfunction str2num
   endian little
    set TONUM 0
    strlen l NUM
    for s = 0 < l
        set EXP l
        math EXP -= s
        math EXP -= 1
        set B 0xA
        math B p EXP
        getVarChr T NUM s
        math T -= 0x30
        math T *= B
        math TONUM += T
    next s
    set NUM TONUM
endfunction

startfunction decompress
   get DUMMY long 1
   get HEADERSIZE long 1
   reverselong HEADERSIZE
   get BLOCKS long 1
   get UNK long 1
   get ZSIZE long 1
   get SIZE long 1
   putVarChr MEMORY_FILE SIZE 0
   log MEMORY_FILE 0 0
   for k = 1 <= BLOCKS
      get ZSIZE long 1
      get ZOFFSET long 1
      math ZOFFSET += OFFSET # relative to file start
      set SIZE ZSIZE
      math SIZE *= 0x200
      append
      clog MEMORY_FILE ZOFFSET ZSIZE SIZE 1
      append
   next k
endfunction