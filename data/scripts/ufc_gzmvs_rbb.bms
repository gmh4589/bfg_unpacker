# UFC Sudden impact (PS2) GZMVS.RBB
#   stereo files may not sound correctly
# script for QuickBMS http://quickbms.aluigi.org

idstring "siff"
get siff_SIZE long
math CHUNK_SIZE = 0
savepos CHUNK_OFF
do
    math CHUNK_OFF + CHUNK_SIZE # leave it here, not at the end
    goto CHUNK_OFF
    getdstring CHUNK_TYPE 4
    get CHUNK_SIZE long
    savepos CHUNK_OFF
    if CHUNK_TYPE u== "RIDX"
        get ZERO long
        get NAMES_SIZE long
        savepos TMP
        xmath FILES "(CHUNK_SIZE - (TMP - CHUNK_OFF)) / (4 + 4)"
        for i = 0 < FILES
            get OFFSET long
            get SIZE long
            putarray 0 i OFFSET
            putarray 1 i SIZE
        next i
    endif
while CHUNK_TYPE u!= "RBB0"

padding 0x800
savepos BASE_OFF

comtype zlib_noerror
if NAMES_SIZE & 0x80000000
    math NAMES_SIZE & 0x7fffffff
    clog MEMORY_FILE BASE_OFF NAMES_SIZE NAMES_SIZE
else
    log MEMORY_FILE BASE_OFF NAMES_SIZE
endif

get MEM_SIZE asize MEMORY_FILE
do
    get TMP line MEMORY_FILE
    if TMP & "="    # ID=gzmvs
    elif TMP & ";"  # comment
    else
        string ELEMENTS S TMP NAME CHANNELS
        if ELEMENTS >= 2
            putarray 2 -1 NAME
            putarray 3 -1 CHANNELS
        endif
    endif
    savepos TMP MEMORY_FILE
while TMP != MEM_SIZE

for i = 0 < FILES
    getarray OFFSET     0 i
    getarray SIZE       1 i
    getarray NAME       2 i
    getarray CHANNELS   3 i
    math OFFSET + BASE_OFF
    string NAME p "channels_%d/%s" CHANNELS NAME

    #log NAME OFFSET SIZE

    math FREQUENCY = 44100
    string NAME + "." # give SShd extension
    callfunction DUMP_VAG 1
next i

# you must provide the following values: OFFSET SIZE FREQUENCY and CHANNELS
startfunction DUMP_VAG
    log MEMORY_FILE 0 0
    put 0x64685353 long MEMORY_FILE
    put 0x18 long MEMORY_FILE
    put 0x10 long MEMORY_FILE
    put FREQUENCY long MEMORY_FILE
    put CHANNELS long MEMORY_FILE
    xmath TMP "SIZE / CHANNELS"
    put TMP long MEMORY_FILE
    put 0 long MEMORY_FILE
    put 0xffffffff long MEMORY_FILE
    put 0x64625353 long MEMORY_FILE
    put SIZE long MEMORY_FILE
    get TMP asize MEMORY_FILE
    log NAME 0 TMP MEMORY_FILE
    append
    log NAME OFFSET SIZE
    append
endfunction
