# RedSpider and RedSpider2 mp3_,png_,jpg_ decrypter
# script for QuickBMS http://quickbms.aluigi.org

set MEMORY_FILE10 string "
# RC4 working without knowing the original key

typedef struct
{
    int x;
    int y;
    unsigned char m[256];
}
arc4_context;

void arc4_crypt_raw(unsigned char *raw, unsigned char *buf, int buflen )
{
    arc4_context ctx_raw;
    arc4_context *ctx = &ctx_raw;
    int i, x, y, a, b;
    unsigned char *m;

    x = ctx->x;
    y = ctx->y;
    m = ctx->m;

    //
    x = y = 0;
    for(i = 0; i < 256; i++) m[i] = raw[i];
    //

    for( i = 0; i < buflen; i++ )
    {
        x = ( x + 1 ) & 0xFF; a = m[x];
        y = ( y + a ) & 0xFF; b = m[y];

        m[x] = (unsigned char) b;
        m[y] = (unsigned char) a;

        buf[i] = (unsigned char)
            ( buf[i] ^ m[(unsigned char)( a + b )] );
    }

    ctx->x = x;
    ctx->y = y;
}
"

get EXT extension
if EXT & "_"
    # encrypted, except for ini_
else
    cleanexit
endif

get PATH FULLBASENAME
if PATH & "RedSpider2"  # "Exiled"
    set RAW_CONTEXT binary "\xa7\x82\x77\x97\x72\x63\x87\xe8\xa1\x9d\xe4\x9e\x4e\xb4\xf6\x5a\xcb\xbb\x14\x08\x10\x1b\x4a\xc6\x3d\x0c\x1d\x21\x88\x25\xa0\x2b\x1c\x65\x84\xf1\x8c\x37\xb5\x41\x69\xe5\x44\x58\x1a\x79\xf7\xed\x19\xb0\x5c\x17\xc0\xa3\x24\xc5\xb2\x18\x2c\x5d\x8f\x70\xfb\x00\x6a\xde\x8e\x6c\xbd\x15\xea\x23\x57\x6e\x1f\x68\xfd\x05\xa8\x50\x86\xd4\x8d\x61\xd2\xd6\x39\x60\x92\x55\xcc\x95\x33\xd7\xe9\x4d\xab\x71\xb7\x51\x6b\x8a\x53\xaf\x98\x2d\x01\x8b\x4c\x66\x22\xfc\xf4\xcd\xa6\xa5\xdd\x0e\xc2\xbe\xf8\xbc\x30\x7e\x9b\x3b\x09\x74\x48\x0b\x3f\x46\x43\x96\xaa\x0a\xfe\x0f\xb6\xe3\x5f\x4f\x7b\xd0\xd5\x94\x31\xc7\x89\x34\xeb\x5b\xc9\x83\x04\xc8\x03\x7a\x2a\xd8\xe2\xf2\xe0\xad\x0d\x9c\x3a\x67\x16\xda\xdf\x02\xba\xff\x91\x26\x76\xa4\xdb\xe7\x2f\x90\xe6\xc3\x75\xee\xf0\xd1\xb1\xd3\x32\xbf\xc4\xc1\x56\xcf\x2e\xec\xce\xca\x36\x6d\x4b\x73\x54\x12\xac\xd9\xf9\x45\x13\xdc\x78\x1e\x64\xb3\xa2\x49\x9f\x81\x6f\x52\xb8\xa9\x20\x93\x35\x06\x80\x3e\x85\x7f\x47\x62\x5e\xfa\xae\x9a\x7c\x42\x59\x28\x07\xe1\x99\x27\x40\x3c\xef\xf3\x11\xf5\x7d\x38\xb9\x29"
else
    set RAW_CONTEXT binary "\x5f\x1d\x4b\x33\xc2\xf8\x42\x9a\x38\x5e\x14\xce\x28\xfe\x63\x37\x3c\x22\x72\xcd\xbe\x64\xc8\x44\x74\xdc\xf4\x3f\x54\x78\x91\xf9\x8b\x07\x84\xc0\x1e\xa0\xd8\xad\xb9\x88\x02\x24\x03\xfc\x8e\xe0\xf0\x2e\xbf\xe7\xb3\x87\xac\xba\x3b\x81\x50\xb2\xde\x05\x30\x57\xc1\x61\x79\x6b\x26\x67\xf5\x16\x36\x1c\x73\x04\x55\xca\x01\x2c\x7f\x71\x3a\x00\x8c\xcf\x86\xab\x6e\x51\x9c\x75\xdb\x82\x7d\x6c\xf7\x85\xa7\x3d\x34\x5d\x4a\xfa\x92\xaf\xa6\x3e\x95\x52\x23\xb1\x09\x7e\x06\xaa\x8f\x83\x77\xd4\x56\x9b\x0e\xda\xb7\xa1\x0f\x8d\x65\xeb\x43\xea\xdd\x32\x0a\x08\xa8\x68\x7c\x98\x6a\xe5\xbd\x48\x1f\x13\xd5\x93\xf1\x9f\x45\x60\x1a\xfd\x0d\x2f\x4d\x0b\x4f\x76\x2a\xc7\x5a\x5c\x49\xbc\x5b\x96\x10\xa9\x29\x39\xa3\x94\x17\xb6\x35\xe3\x0c\xdf\x41\x97\xff\x19\x18\xed\x40\xd9\x53\xa5\x46\xb4\xf6\x6d\xc9\x58\xec\xef\x2b\xe2\x8a\xcc\xc6\xc4\xd0\x9d\xae\x89\x80\x31\xee\xf3\xcb\xfb\xf2\x99\x7a\x59\xe9\xbb\xd1\xe1\x62\x15\xd2\x7b\xc5\x2d\x21\x47\x69\x70\xa4\xb8\x20\x4c\xb5\x66\xd6\xd3\xb0\xa2\x90\x9e\x11\xe6\x12\xd7\x25\xe4\x6f\xe8\x4e\x27\x1b\xc3"
endif

get SIZE asize
get NAME filename
string NAME - 1 # remove the final _

math ENCRYPTED_SIZE = 0x360
if SIZE < ENCRYPTED_SIZE
    math ENCRYPTED_SIZE = SIZE
endif

encryption calldll "MEMORY_FILE10 arc4_crypt_raw tcc RET RAW_CONTEXT #INPUT# #INPUT_SIZE#"
log NAME 0 ENCRYPTED_SIZE
append
math SIZE - ENCRYPTED_SIZE
encryption "" ""
log NAME ENCRYPTED_SIZE SIZE
append
