# The Incredible Hulk (2008) (index.ind / arc) (script 0.2)

get EXT extension
if EXT == "ind"

    get FOLDERS long
    endian guess FOLDERS
    math FOLDERS / 2
    for FOLDER = 0 < FOLDERS
        get NAME_OFF long
        get INFO_OFF long

        savepos BCK_OFF
        goto NAME_OFF
        get ARC_NAME string
        string TMP p "%s.arc" ARC_NAME
        open FDSE TMP 1 EXISTS
        if EXISTS == 0
            string TMP p "%.8s.arc" ARC_NAME    # 8.3
            open FDSE TMP 1 EXISTS
        endif
        if EXISTS != 0
            goto INFO_OFF
            get FILES long
            for i = 0 < FILES
                get SOME_CRC long
                putarray 0 i SOME_CRC
            next i
            for i = 0 < FILES
                get OFFSET long
                get SIZE long
                get ZERO long
                getarray SOME_CRC 0 i
                string NAME p "%s/%08x." ARC_NAME SOME_CRC
                log NAME OFFSET SIZE 1
            next i
        endif
        goto BCK_OFF
    next FOLDER

else    # ARC

    get OFFSET long
    goto OFFSET
    get FILES long
    endian guess FILES
    endian save CURRENT_ENDIAN
    for i = 0 < FILES
        get SOME_CRC long
        get OFFSET long
        get SIZE long
        get NAME string
        get TSTAMP longlong
        if CURRENT_ENDIAN != 0
            get DUMMY long
        endif
        log NAME OFFSET SIZE
    next i

endif
