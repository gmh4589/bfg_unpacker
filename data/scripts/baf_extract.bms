#baf_extract.bms (X360 only!)
#Extracts the XMA files from sound banks and adds a valid header for toWAV. Frequency and channels is taken from the file info table of course.

idstring "BANK"
endian big
get UNK long
get UNK long
get FILES long
goto 0x34

for i = 1 <= FILES
   savepos OFF
   idstring "WAVE"
   get INFOSIZE long
   get HEADERSIZE long
   getDstring NAME 0x20
   string NAME += ".xma"
   get OFFSET long
   get SIZE long
   get UNK long
   get UNK long
   get FREQ long
   get UNK long
   get CH long
   endian little
   callfunction addXMAheader 1
   endian big
   get SIZE asize MEMORY_FILE
   log NAME 0 SIZE MEMORY_FILE
   math OFF += INFOSIZE
   goto OFF
next i

startfunction addXMAheader
   set MEMORY_FILE binary "\x52\x49\x46\x46\xb8\x59\xa7\x00\x57\x41\x56\x45\x66\x6d\x74\x20\x20\x00\x00\x00\x65\x01\x10\x00\xd6\x10\x00\x00\x01\x00\x00\x03\xe3\x9a\x00\x00\x80\xbb\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x02\x02\x00\x64\x61\x74\x61\x00\x58\xa7\x00"
   set RIFFSIZE SIZE
   math RIFFSIZE += 0x34
   putVarChr MEMORY_FILE 0x04 RIFFSIZE long
   putVarChr MEMORY_FILE 0x24 FREQ long
   putVarChr MEMORY_FILE 0x31 CH byte
   putVarChr MEMORY_FILE 0x38 SIZE long
   append
   log MEMORY_FILE OFFSET SIZE
   append
endfunction