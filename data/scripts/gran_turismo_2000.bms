# Gran Turismo 2000
# script for QuickBMS http://quickbms.aluigi.org

endian big
for
    get NAMESZ byte
    if NAMESZ == 0
        break
    endif
    getdstring NAME NAMESZ
    get OFFSET long
    get SIZE long
    math OFFSET * 0x800

    set EXT extension NAME
    if EXT == "" || EXT == "dat"    # seattle, car.dat and so on
        string NAME + /
        savepos TMP
        goto OFFSET
        endian save CURRENT_ENDIAN
        endian little
        math BASE_OFF = OFFSET
        get ZERO long
        for i = 0 >= 0
            get OFFSET long
            if OFFSET == 0
                math FILES = i
                putarray 0 i SIZE
                math i = -2 # break
            else
                putarray 0 i OFFSET
            endif
        next i
        for i = 0 < FILES
            getarray OFFSET 0 i
            math i + 1
            getarray SIZE   0 i
            math SIZE - OFFSET
            math OFFSET + BASE_OFF
            log NAME OFFSET SIZE
        next
        goto TMP
        endian CURRENT_ENDIAN
    else
        log NAME OFFSET SIZE
    endif
next
