# Seeing as Lords of Shadow 2 (PS3-DEMO) (PACKED format) 0.2
#
# Written by Ekey (h4x0r)
# http://forum.xentax.com
#
# script for QuickBMS http://quickbms.aluigi.org

comtype unzip_dynamic

idstring "BFPK"
get VERSION long

if VERSION = 256
    get FILES long
elseif VERSION = 258
    get TABLESIZE long
    get FILES long
endif

for i = 0 < FILES
    get NSIZE long
    getdstring NAME NSIZE
    get SIZE long
    get OFFSET long
    get NULL long
    savepos TEMP

    if VERSION = 256
        log NAME OFFSET SIZE
    elseif VERSION = 258
   
        goto OFFSET
        get FULLZSIZE long
        savepos CHUNKOFFSET
      
        log MEMORY_FILE 0 0
        append
        set TEMPZSIZE long 0

        #Ugly part for decompress chunks :)

        do
            goto CHUNKOFFSET
            get CHUNKZSIZE long
            math TEMPZSIZE += CHUNKZSIZE
            math TEMPZSIZE += 4
            savepos OFFSET
            clog MEMORY_FILE OFFSET CHUNKZSIZE CHUNKZSIZE
            math CHUNKOFFSET += CHUNKZSIZE
            math CHUNKOFFSET += 4
        while TEMPZSIZE < FULLZSIZE
      
        append
        log NAME 0 SIZE MEMORY_FILE
        goto TEMP
    endif
next i