# THPS HED/WAD (script 0.2.1)
# script for QuickBMS http://quickbms.aluigi.org

# some files use the alignment 1, you have to set this variable to 1
math ALIGN = 0x800

# 0: normal extraction
# 1: offset-based extraction (some users say that it's better for audio)
math EXTRACTION_MODE = 1

open FDDE "HED"
open FDDE "WAD" 1
get HED_SIZE asize
for i = 0
    get OFFSET long
    if OFFSET == 0xffffffff
        break
    endif
    get SIZE long
    get NAME string
    padding 4

    putarray 0 i OFFSET
    putarray 1 i SIZE
    putarray 2 i NAME
next i
math FILES = i

get WAD_SIZE asize 1
putarray 0 i WAD_SIZE

sortarray 0 1

if ALIGN != 1
    math i - 1
    getarray TMP  0 i
    getarray TMP2 1 i
    math TMP + TMP2
    math TMP x ALIGN    # padding, just in case
    if TMP u>= WAD_SIZE
        math ALIGN = 1
    endif
endif

for i = 0 < FILES
    getarray OFFSET 0 i
    math OFFSET * ALIGN
    getarray SIZE   1 i
    getarray NAME   2 i
    math i + 1
    if EXTRACTION_MODE != 0
        getarray SIZE 0 i
        math SIZE * ALIGN
        math SIZE - OFFSET
    endif
    log NAME OFFSET SIZE 1
next
